# $Id: PKGBUILD 270541 2016-06-23 21:00:00Z seblu $
# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=file
pkgver=5.28
pkgrel=1
pkgdesc='File type identification utility'
arch=('x86_64' 'armv7h' 'aarch64')
license=('custom')
groups=('base' 'base-devel')
url='http://www.darwinsys.com/file/'
depends=('glibc' 'zlib')
source=("ftp://ftp.astron.com/pub/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('3f7771424aa855f32094b49571e19b33')

prepare() {
  cd $pkgname-$pkgver
  # apply patch from the source array (should be a pacman feature)
  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      msg2 "Applying patch $filename"
      patch -p1 -N -i "$srcdir/$filename"
    fi
  done
  :
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --datadir=/usr/share/file --enable-fsect-man5
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
