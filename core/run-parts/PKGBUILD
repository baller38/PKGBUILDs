# $Id: PKGBUILD 260322 2016-02-27 15:51:35Z heftig $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=run-parts
pkgver=4.7
pkgrel=1
pkgdesc='run scripts or programs in a directory'
arch=('i686' 'x86_64')
url='http://packages.qa.debian.org/d/debianutils.html'
license=('GPL')
depends=('glibc')
source=("https://sources.archlinux.org/other/run-parts/debianutils_${pkgver}.tar.xz")
sha256sums=('a269cacd40f52f2fa5d5636357714a49e8538459c16d77772efaa23711fe53d9')

build() {
	cd $srcdir/debianutils-$pkgver
	./configure --prefix=/usr
	make run-parts
}

package() {
	cd $srcdir/debianutils-$pkgver
	install -D -m755 run-parts $pkgdir/usr/bin/run-parts
	install -D -m644 run-parts.8 $pkgdir/usr/share/man/man8/run-parts.8
	for l in po4a/*/run-parts.8; do
		install -D -m644  ${l} $pkgdir/usr/share/man/${l:5:2}/man8/run-parts.8
	done
}
