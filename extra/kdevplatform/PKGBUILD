# $Id: PKGBUILD 266614 2016-04-28 22:00:05Z arojas $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=kdevplatform
pkgver=1.7.3
_pkgver=4.7.3
pkgrel=2
pkgdesc='A C/C++ development platform for KDE'
arch=('i686' 'x86_64')
url='http://www.kdevelop.org/'
license=('GPL')
depends=('kdelibs' 'subversion' 'qjson' 'grantlee-qt4')
makedepends=('cmake' 'automoc4' 'boost')
source=("http://download.kde.org/stable/kdevelop/${_pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('7201629fb4fa9bec83def4bf691f4eda41bfc555')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
