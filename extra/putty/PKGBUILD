# $Id: PKGBUILD 268559 2016-05-25 20:14:18Z arojas $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=putty
pkgver=0.67
pkgrel=1
pkgdesc="A terminal integrated SSH/Telnet client"
arch=('i686' 'x86_64')
url="http://www.chiark.greenend.org.uk/~sgtatham/putty/"
license=('MIT')
depends=('gtk2')
source=(http://the.earth.li/~sgtatham/putty/${pkgver}/${pkgname}-${pkgver}.tar.gz
        putty-${pkgver}.tar.gz.sig::http://the.earth.li/~sgtatham/putty/${pkgver}/putty-${pkgver}.tar.gz.gpg)
sha1sums=('132ff74266e590a007f86cbb4bea5642db7bdd76'
          'c3002f9062f625672bb419d15debcdc7f75f4fb6')
validpgpkeys=('0054DDAA8ADA15D2768A6DE79DFE2648B43434E4')

build() {
  cd ${pkgname}-${pkgver}/unix
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}/unix
  make DESTDIR="${pkgdir}" install
  install -D -m644 ../LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
