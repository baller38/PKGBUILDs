# $Id: PKGBUILD 264473 2016-04-10 11:41:08Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-characters
pkgver=3.20.1
pkgrel=1
pkgdesc="Simple utility application to find and insert unusual characters"
arch=(i686 x86_64)
url="http://gnome.org"
license=('GPL2')
depends=(gjs gtk3 libunistring gnome-desktop)
makedepends=(intltool gobject-introspection)
install=gnome-characters.install
groups=('gnome-extra')
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('6891eed27a5b023200992540266a9216d081eef890d6d0836380dc3c0033857c')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
