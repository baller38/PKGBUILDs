# $Id: PKGBUILD 257607 2016-01-07 14:44:11Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=libpst
pkgname=('libpst' 'libpst-docs')
pkgver=0.6.66
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.five-ten-sg.com/libpst/"
license=('GPL')
makedepends=('python2' 'boost' 'libgsf')
source=(http://www.five-ten-sg.com/libpst/packages/${pkgbase}-${pkgver}.tar.gz)
sha1sums=('02fe2035df93e4006ac92df20038c9c82f77f13e')

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man \
      --disable-static --enable-libpst-shared --disable-dii PYTHON_VERSION=2
  make
}

package_libpst() {
  pkgdesc="Outlook .pst file converter"
  depends=('gcc-libs' 'libgsf')
  optdepends=('boost-libs: for libpst python interface')
  options=('!docs')

  cd $pkgbase-$pkgver
  make DESTDIR="${pkgdir}" install
}

package_libpst-docs() {
  pkgdesc="Documentation for Outlook .pst file converter"
  options=('docs')

  cd $pkgbase-$pkgver
  make DESTDIR="${pkgdir}" install-htmlDATA
  cd html
  make DESTDIR="${pkgdir}" install-htmldevelDATA
}
