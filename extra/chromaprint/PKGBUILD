# $Id: PKGBUILD 260284 2016-02-27 14:10:03Z alucryd $
# Contributor: Wieland Hoffmann <the_mineo@web.de>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=chromaprint
pkgver=1.3.1
pkgrel=1
pkgdesc='Library that implements a custom algorithm for extracting fingerprints from any audio source'
url='https://acoustid.org/chromaprint'
arch=('i686' 'x86_64')
license=('LGPL')
depends=('ffmpeg')
makedepends=('cmake')
source=("https://bitbucket.org/acoustid/${pkgname}/downloads/${pkgname}-${pkgver}.tar.gz")
sha1sums=('439c5d34a3ff46bcdb54cbcb613da175eb4d83c5')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	cmake \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DBUILD_EXAMPLES=ON \
		.
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
