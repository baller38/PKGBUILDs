# $Id: PKGBUILD 270343 2016-06-20 19:24:21Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwallet
pkgver=5.23.0
pkgrel=1
pkgdesc='Secure and unified container for user passwords'
arch=('i686' 'x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('knotifications' 'kiconthemes' 'kservice' 'gpgmepp')
makedepends=('extra-cmake-modules' 'python' 'kdoctools' 'boost')
optdepends=('kwalletmanager: Configuration GUI')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
md5sums=('37715313db7117da3eabc163ce8c9f0b')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
