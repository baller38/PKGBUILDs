# $Id: PKGBUILD 269810 2016-06-14 16:48:04Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kalgebra
pkgver=16.04.2
pkgrel=1
pkgdesc="Graph Calculator"
url="http://kde.org/applications/education/kalgebra/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('analitza' 'kio' 'qt5-webkit' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'glu')
replaces=('kdeedu-kalgebra')
conflicts=('kdeedu-kalgebra' 'kdeedu-analitza')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kalgebra-${pkgver}.tar.xz")
sha1sums=('968018460d4bad94f12ad2d3072f15f6e4e0e72c')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kalgebra-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
