# $Id: PKGBUILD 268861 2016-06-03 07:01:42Z remy $
# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: Firmicus <francois.archlinux.org>
# Contributor: bender02 at gmx dot com

pkgname=asymptote
pkgver=2.38
pkgrel=1
pkgdesc="A vector graphics language (like metapost)"
arch=('i686' 'x86_64')
url="http://asymptote.sourceforge.net/"
license=("GPL3")
depends=('texlive-core' 'gc' 'freeglut' 'glu' 'gsl' 'fftw' 'libsigsegv')
makedepends=('ghostscript' 'imagemagick'
             'mesa'               # For OpenGL headers
             'texlive-plainextra' # For texinfo
            )
optdepends=('python2:           for the xasy GUI'
            'python2-pillow:    for the xasy GUI'
            'tix:               for the xasy GUI')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.src.tgz")
sha1sums=('911dc852a788f19300ae44162dbc12539de9e0b3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --enable-gc=/usr \
              --prefix=/usr \
              --with-latex=/usr/share/texmf/tex/latex \
              --with-context=/usr/share/texmf/tex/context \
              --enable-offscreen
  make all
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check-all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install-all
  sed -i -e 's@env python@env python2@' ${pkgdir}/usr/share/asymptote/GUI/*.py
  # this dir contains png files that are already embedded in the pdf documentation:
  rm -rf ${pkgdir}/usr/share/info/asymptote
}
