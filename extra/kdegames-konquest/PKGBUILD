# $Id: PKGBUILD 269848 2016-06-14 16:59:32Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-konquest
pkgver=16.04.2
pkgrel=1
pkgdesc="The KDE version of Gnu-Lactic"
url="http://kde.org/applications/games/konquest/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/applications/${pkgver}/src/konquest-${pkgver}.tar.xz")
sha1sums=('6a2c5c8dd97193008012d8b733c7694afc38e126')

build() {
  mkdir -p build
  cd build
  cmake ../konquest-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
