# $Id: PKGBUILD 265842 2016-04-22 19:54:58Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=w3m
pkgver=0.5.3.git20160413
pkgrel=1
pkgdesc='Text-based Web browser, as well as pager'
url='http://w3m.sourceforge.net/'
license=('custom')
arch=('i686' 'x86_64')
makedepends=('git' 'imlib2')
optdepends=('imlib2: for graphics support') 
depends=('openssl' 'gc' 'ncurses' 'gpm')
source=("git://anonscm.debian.org/collab-maint/w3m.git#commit=9a1b7a25b32e426cebb4ee048133861ca8430d5e")
sha1sums=('SKIP')

build() {
	cd "${srcdir}/${pkgname}"
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib \
		--enable-image=x11,fb \
		--with-imagelib=imlib2 \
		--with-termlib=ncurses \
		--disable-w3mmailer \

	make
}

package() {
	cd "${srcdir}/${pkgname}"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}"/usr/share/{doc,licenses}/"${pkgname}"
	find doc/* | grep -v CVS | xargs -i install -m644 "{}" "${pkgdir}/usr/share/doc/${pkgname}"
	ln -s ../../doc/"${pkgname}"/README "${pkgdir}/usr/share/licenses/${pkgname}"
}
