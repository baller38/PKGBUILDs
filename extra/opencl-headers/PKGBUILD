# $Id: PKGBUILD 259861 2016-02-16 22:14:21Z lcarlier $
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Sylvain HENRY <hsyl20@yahoo.fr>

pkgname=opencl-headers
pkgver=2.1.20151224
_clbasever=2.1
pkgrel=1
epoch=2
pkgdesc='OpenCL (Open Computing Language) header files'
arch=('any')
url='http://www.khronos.org/registry/cl/'
license=('custom')
source=("https://sources.archlinux.org/other/community/${pkgname}/${pkgname}-${pkgver}.src.tar.xz"
        'LICENSE.txt')
_headers=("http://www.khronos.org/registry/cl/api/${_clbasever}/opencl.h"
          "http://www.khronos.org/registry/cl/api/${_clbasever}/cl_platform.h"
          "http://www.khronos.org/registry/cl/api/${_clbasever}/cl.h"
          "http://www.khronos.org/registry/cl/api/${_clbasever}/cl_ext.h"
          "http://www.khronos.org/registry/cl/api/${_clbasever}/cl_egl.h"
          "http://www.khronos.org/registry/cl/api/${_clbasever}/cl_gl.h"
          "http://www.khronos.org/registry/cl/api/${_clbasever}/cl_gl_ext.h"
          "http://www.khronos.org/registry/cl/api/${_clbasever}/cl2.hpp"
          "http://www.khronos.org/registry/cl/api/${_clbasever}/cl.hpp") # compatibility for OCL 1.2
sha1sums=('6fae079bd2b02b1ea5312f61ff817c8f541ceb4f'
          '98abb35b2eca82d0fc19db6d28fcc7bd20be0655')

# source PKGBUILD && mksource
mksource() {
   cd "${srcdir}"
   _dirname=${pkgname}-${pkgver}
   mkdir ${_dirname}

   pushd ${_dirname}
   for _h in ${_headers[@]}; do
      wget ${_h}
   done
   popd

   tar -cJv --exclude=CVS -f ${_dirname}.src.tar.xz ${_dirname}
   rm -rf ${_dirname}
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"

   install -dm755 "${pkgdir}"/usr/include/CL

   for h in $(ls -1 *.h *.hpp); do
      install -m 644 ${h} "${pkgdir}"/usr/include/CL/
   done

   install -D -m644 ../LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
