# $Id: PKGBUILD 269858 2016-06-14 17:02:23Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kubrick
pkgver=16.04.2
pkgrel=1
pkgdesc="Based on the famous Rubik's Cube"
url="http://kde.org/applications/games/kubrick/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4' 'glu')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kubrick-${pkgver}.tar.xz")
sha1sums=('9729a7f5f2a94ae08dd59e299676e23370f67f2a')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kubrick-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
