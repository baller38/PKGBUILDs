# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: Paul Ezvan <paul@ezvan.fr>

pkgname=terminology
pkgver=0.9.1
pkgrel=2
pkgdesc="EFL based terminal emulator"
arch=('i686' 'x86_64')
url="https://www.enlightenment.org/about-terminology"
license=('BSD')
depends=('elementary')
optdepends=('emotion_generic_players: video support')
source=("http://download.enlightenment.org/rel/apps/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('5b3dd4f0ce8c8a2384c8019843664cfb71f0bc37')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export CFLAGS="${CFLAGS} -fvisibility=hidden"
  
  ./configure \
    --prefix=/usr

  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # install license files
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING" \
        "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
