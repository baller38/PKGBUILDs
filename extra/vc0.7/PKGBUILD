# $Id: PKGBUILD 268761 2016-05-31 06:37:09Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: PedsXing <pedsxing at gmx dot net>

pkgname=vc0.7
pkgver=0.7.5
pkgrel=1
pkgdesc="A library to ease explicit vectorization of C++ code (legacy version)"
url='https://github.com/VcDevel/Vc'
arch=(i686 x86_64)
license=(LGPL3)
makedepends=(cmake)
conflicts=(vc)
source=("https://github.com/VcDevel/Vc/releases/download/$pkgver/Vc-$pkgver.tar.gz")
md5sums=('59854e2381b78d602b4a00e459e3d333')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../Vc-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DTARGET_ARCHITECTURE=generic
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
