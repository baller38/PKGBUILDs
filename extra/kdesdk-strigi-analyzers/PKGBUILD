# $Id: PKGBUILD 269918 2016-06-14 17:19:10Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-strigi-analyzers
pkgver=16.04.2
pkgrel=1
pkgdesc='Analyzer plugins for strigi'
url='https://www.kde.org/applications/development/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kdesdk-strigi-analyzers-${pkgver}.tar.xz")
sha1sums=('d27951ec2074c1f4bd515b15f4bfe6700156a252')

build() {
  mkdir -p build
  cd build
  cmake ../kdesdk-strigi-analyzers-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
