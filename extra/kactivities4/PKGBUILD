# $Id: PKGBUILD 255814 2015-12-11 23:32:05Z arojas $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgbase=kactivities4
pkgname=(libkactivities4)
pkgver=4.13.3
pkgrel=3
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/kactivities'
license=('GPL' 'FDL')
makedepends=('kdelibs' 'cmake' 'automoc4' 'mesa')
source=("http://download.kde.org/stable/${pkgver}/src/kactivities-${pkgver}.tar.xz")
sha1sums=('54813db8808da63dda7980711ba5d4e01e56d2b0')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kactivities-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_NepomukCore=OFF
  make
}

package_libkactivities4() {
  pkgdesc='API for using and interacting with the Activity Manager'
  depends=('kdelibs')

  cd build
  make DESTDIR="$pkgdir" install

  # Fix conflicts with kactivities
  rm -r "$pkgdir"/usr/bin
}
