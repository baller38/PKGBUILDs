# $Id: PKGBUILD 264290 2016-04-09 13:16:45Z foutrelis $
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Sergej Chodarev <sergejx@centrum.sk>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=gedit-plugins
pkgver=3.20.0
pkgrel=1
pkgdesc="Plugins for gedit"
arch=(x86_64 i686)
license=(GPL)
url="http://live.gnome.org/GeditPlugins"
depends=(gedit python-dbus python-cairo)
makedepends=(intltool vte3 libgit2-glib itstool zeitgeist gucharmap gobject-introspection vala)
optdepends=('gucharmap: for charmap plugin'
            'vte3: for embedded terminal'
            'libgit2-glib: for git plugin'
            'zeitgeist: for dashboard plugin')
options=('!emptydirs')
install=gedit-plugins.install
source=("https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz")
sha256sums=('d4902af5f9f728e0d692ca8c2f7a9f88f6c2ab8c9be037238ffb7d3aebe757bf')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install
}

# vim:set ts=2 sw=2 et:
