# $Id: PKGBUILD 270157 2016-06-17 18:42:27Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=grilo-plugins
pkgver=0.3.2
pkgrel=1
pkgdesc="Plugins for Grilo"
url="http://www.gnome.org"
arch=(i686 x86_64)
license=(LGPL)
depends=(grilo lua libmediaart libgdata libtracker-sparql gom libdmapsharing chromaprint
         gst-plugins-bad)
makedepends=(intltool itstool yelp-tools)
optdepends=('dleyna-server: DLNA plugin'
            'tracker: Tracker plugin')
options=('!emptydirs')
groups=('gnome')
source=(https://download.gnome.org/sources/${pkgname}/${pkgver:0:3}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('791b89289781272c001545931a8f58f499d14e46e038a9caa82dfe2494301afd')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
#  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
