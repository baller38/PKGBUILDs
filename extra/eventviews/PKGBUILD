# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=eventviews
pkgver=16.04.2
pkgrel=1
pkgdesc="Library for creating events"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(calendarsupport kdgantt2)
makedepends=(extra-cmake-modules kdoctools python boost qt5-tools kdesignerplugin)
conflicts=('libkdepim<16')
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('2afcdacaf80383134c05d27c8241710d')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
