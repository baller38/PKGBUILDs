# $Id: PKGBUILD 264362 2016-04-09 13:19:16Z foutrelis $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=hitori
pkgver=3.20.0
pkgrel=1
pkgdesc="GTK+ application to generate and let you play games of Hitori"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Apps/Hitori"
license=('GPL')
depends=('gtk3')
groups=('gnome-extra')
makedepends=('intltool' 'itstool')
install=hitori.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('877cae48144b7f6908fde55104f589788b42acaa5956d62fd32b4026fde3769d')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
