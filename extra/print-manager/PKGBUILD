# $Id: PKGBUILD 269933 2016-06-14 17:23:28Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=print-manager
pkgver=16.04.2
pkgrel=1
pkgdesc="A tool for managing print jobs and printers"
arch=('i686' 'x86_64')
url='https://www.kde.org/applications/utilities/'
license=('GPL' 'LGPL' 'FDL')
depends=('libcups' 'kcmutils')
makedepends=('extra-cmake-modules' 'python' 'kdoctools' 'plasma-framework')
optdepends=('system-config-printer: auto-detect the printer driver')
replaces=('kdeutils-print-manager')
conflicts=('kdeutils-print-manager')
groups=('kde-applications' 'kdeutils')
source=("http://download.kde.org/stable/applications/${pkgver}/src/print-manager-${pkgver}.tar.xz")
sha1sums=('e1bd1f73845b3e47f8fc50c0bddb410f74fd85d0')

build() {
  mkdir -p build
  cd build
  cmake ../print-manager-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
