# $Id: PKGBUILD 269364 2016-06-11 20:16:44Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=bzflag
pkgver=2.4.4
pkgrel=1
pkgdesc='Multiplayer 3D tank battle game'
url='http://bzflag.org/'
license=('LGPL')
arch=('i686' 'x86_64')
depends=('curl' 'glew' 'glu' 'sdl' 'c-ares')
source=("https://download.bzflag.org/bzflag/source/${pkgver}/bzflag-${pkgver}.tar.bz2")
sha1sums=('4c3739447cbb16924d1983155340b7a76e3c9e29')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
	cd misc
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 misc/bzflag.desktop "${pkgdir}"/usr/share/applications/bzflag.desktop
	install -Dm644 data/bzflag-48x48.png "${pkgdir}"/usr/share/pixmaps/bzflag-48x48.png
}
