# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgbase=mtr
pkgname=('mtr' 'mtr-gtk')
pkgver=0.86
pkgrel=3
pkgdesc='Combines the functionality of traceroute and ping into one tool'
url="http://www.bitwizard.nl/mtr/"
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('ncurses' 'gtk2')
source=(${pkgbase}-${pkgver}.tar.gz::ftp://ftp.bitwizard.nl/mtr/${pkgbase}-${pkgver}.tar.gz)
sha512sums=('e649825ab77987e369e0cd353a502b31f4aafa54a934467db907cb171af45b11bd0a8725d567d923422f32b886ad272b42151514c71893f0ba947f8d18719583')

prepare() {
    cp -ra ${pkgbase}-${pkgver}{,-cli}
}

build() {
  (cd ${pkgbase}-${pkgver}-cli
    ./configure --prefix=/usr --without-gtk --sbindir=/usr/bin
    make
  )
  (cd ${pkgbase}-${pkgver}
    ./configure --prefix=/usr --sbindir=/usr/bin
    make
  )
}

package_mtr() {
  pkgdesc='Combines the functionality of traceroute and ping into one tool (CLI version)'
  depends=('ncurses')
  cd ${pkgbase}-${pkgver}-cli
  make DESTDIR="${pkgdir}" install
}

package_mtr-gtk() {
  pkgdesc='Combines the functionality of traceroute and ping into one tool (GTK version)'
  depends=('ncurses' "gtk2")
  conflicts=('mtr')
  provides=("mtr=${pkgver}")
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
