# $Id: PKGBUILD 270885 2016-07-03 12:39:56Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Gour <Gour <gour@mail.inet.hr>

pkgname=libisoburn
pkgver=1.4.4 #.pl01
pkgrel=1
pkgdesc="frontend for libraries libburn and libisofs"
url="http://libburnia-project.org"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('libburn' 'libisofs' 'readline')
optdepends=('tk:      for xorriso-tcltk frontend'
            'bwidget: for xorriso-tcltk frontend')
provides=('xorriso' 'xorriso-tcltk')
source=(http://files.libburnia-project.org/releases/${pkgname}-${pkgver}.tar.gz{,.sig})
md5sums=('3e1d789a0066d8deb27b672bac23e508'
         'SKIP')
validpgpkeys=('44BC9FD0D688EB007C4DD029E9CBDFC0ABC0A854') # Thomas Schmitt <scdbackup@gmx.net>

build() {
  cd ${pkgname}-${pkgver/.pl01/}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver/.pl01/}
  make DESTDIR=${pkgdir} install
}
