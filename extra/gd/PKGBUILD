# $Id: PKGBUILD 267012 2016-05-06 08:38:35Z pierre $

pkgname=gd
pkgver=2.1.1
pkgrel=4
pkgdesc="Library for the dynamic creation of images by programmers"
arch=('i686' 'x86_64')
url="http://www.libgd.org/"
license=('custom')
depends=('fontconfig' 'libvpx' 'libxpm' 'libtiff')
makedepends=('git')
optdepends=('perl: bdftogd script')
source=("${pkgname}::git+https://github.com/libgd/libgd.git#tag=${pkgname}-${pkgver}"
        'gd-2.1.1-libvpx-1.4.0.patch' 'CVE-2016-3074.patch')
md5sums=('SKIP'
         '9114dd8259aaa88b0a09188fe7b19afc'
         '60d9ef94a60d9a77232b79da4b80626e')

prepare() {
  cd ${pkgname}
  ./bootstrap.sh
  patch -p1 -i "${srcdir}/gd-2.1.1-libvpx-1.4.0.patch"
  patch -p1 -i "${srcdir}/CVE-2016-3074.patch"
}

build() {
  cd ${pkgname}
  ./configure \
    --prefix=/usr \
    --disable-rpath \
    --with-vpx=/usr \
    --with-tiff=/usr
  make
}

check() {
  cd ${pkgname}
  make check
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
