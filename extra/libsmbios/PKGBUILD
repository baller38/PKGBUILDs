# $Id: PKGBUILD 268358 2016-05-20 13:11:00Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: daniel g. siegel <dgsiegel@gmail.com>

pkgname=libsmbios
pkgver=2.3.0
pkgrel=1
pkgdesc="A library for providing access to as much BIOS information as possible"
arch=('i686' 'x86_64')
url="http://linux.dell.com/libsmbios/main/index.html"
license=('GPL' 'custom')
depends=('gcc-libs')
makedepends=('libxml2' 'python2' 'chrpath' 'doxygen')
optdepends=('python2: tools')
source=(http://linux.dell.com/libsmbios/download/${pkgname}/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.xz)
md5sums=('8f4bef657b04250f077f7ac5f2ecac2c')

build() {
unset MAKEFLAGS
  cd $pkgname-$pkgver
  mkdir build
  cd build
  PYTHON=python2 ../configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc --disable-static
  mkdir -p out/libsmbios_c
  mkdir -p out/libsmbios_c++
  make
}

package() {
  cd ${pkgname}-${pkgver}/build
  make DESTDIR="${pkgdir}" install

  chrpath -d "${pkgdir}/usr/bin/smbios-sys-info-lite"
  install -m755 -d "${pkgdir}/usr/include"
  cp -a ../src/include/* "${pkgdir}/usr/include/"
  cp -a out/public-include/* "${pkgdir}/usr/include/"

  rm -rf "${pkgdir}/etc/yum"
  rm -rf "${pkgdir}/usr/lib/yum-plugins"

  sed -e 's|#!/usr/bin/python|#!/usr/bin/python2|g' \
      -i "${pkgdir}"/usr/bin/smbios-{{wakeup,token,wireless,keyboard,thermal}-ctl,lcd-brightness,rbu-bios-update,passwd,sys-info}

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 ../COPYING-OSL "${pkgdir}/usr/share/licenses/${pkgname}/"

  python2 -m compileall "${pkgdir}/usr/lib/python2.7/site-packages/libsmbios_c/"
}
