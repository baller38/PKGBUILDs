# $Id: PKGBUILD 270633 2016-06-25 09:14:44Z andyrtr $
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: kusakata <shohei atmark kusakata period com>

pkgname=mdds
pkgver=1.2.1
pkgrel=1
pkgdesc="A collection of multi-dimensional data structures and indexing algorithms"
arch=('any')
url="https://gitlab.com/mdds/mdds"
license=('MIT')
checkdepends=('boost')
#source=(https://gitlab.com/mdds/mdds/repository/archive.tar.gz?ref=${pkgver})
source=(http://kohei.us/files/mdds/src/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('1e2f49dfc7b9d444bad07064837099741f4c2d061807173392ad2357116dfc7b')

build() {
  cd ${pkgname}-${pkgver}*
  ./autogen.sh --prefix=/usr
  # make
}

check() {
  cd ${pkgname}-${pkgver}*
  make check
}

package() {
  cd ${pkgname}-${pkgver}*
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
