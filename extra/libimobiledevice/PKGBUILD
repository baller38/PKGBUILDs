# $Id: PKGBUILD 261054 2016-03-06 10:48:14Z pierre $
# Maintainer : Tom Gundersen <teg@jklm.no>
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Gabriel Martinez < reitaka at gmail dot com >

pkgname=libimobiledevice
pkgver=1.2.0
pkgrel=3
pkgdesc="Library that talks the protocols to support iPhone and iPod Touch devices on Linux"
url="http://libimobiledevice.org/"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1')
depends=('libusbmuxd')
makedepends=('python2')
source=(http://libimobiledevice.org/downloads/$pkgname-$pkgver.tar.bz2
        disable-sslv3.patch)
md5sums=('8757900ba7bbe2ef5f54342415d0223e'
         'bac123da4cc67b2f5cc798727e6231a9')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../disable-sslv3.patch
}

build() {
  cd "$pkgname-$pkgver"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
