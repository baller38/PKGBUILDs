# $Id: PKGBUILD 175428 2016-05-15 20:20:35Z stativ $
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=embree
pkgver=2.9.0
pkgrel=1
pkgdesc="A collection of high-performance ray tracing kernels"
arch=('x86_64')
url="https://embree.github.io/"
license=('Apache')
depends=('intel-tbb')
makedepends=('cmake' 'ispc' 'openexr')
source=("$pkgname-${pkgver}.tar.gz::http://github.com/embree/embree/archive/v${pkgver}.tar.gz")
md5sums=('1c6128b2d96b263721e514eb8f5ca0fc')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_TUTORIALS=OFF \
    -DTBB_ROOT=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

