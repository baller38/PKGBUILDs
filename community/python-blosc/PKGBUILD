# $Id: PKGBUILD 170180 2016-04-10 20:11:23Z aginiewicz $
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgbase=python-blosc
pkgname=('python2-blosc' 'python-blosc')
pkgver=1.3.2
pkgrel=1
pkgdesc="A Python wrapper for the extremely fast Blosc compression library"
arch=('i686' 'x86_64')
url="http://python-blosc.blosc.org/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'blosc')
source=("https://pypi.python.org/packages/source/b/blosc/blosc-${pkgver}.tar.gz")
md5sums=('d87100614b8423b542958ba1900c9e7b')

prepare() {
  cd "$srcdir"
  cp -a blosc-${pkgver} blosc-py2-${pkgver}
  cd blosc-py2-${pkgver}

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  msg "Building Python2"
  cd "$srcdir"/blosc-py2-${pkgver}
  python2 setup.py build_ext --inplace --blosc=/usr
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/blosc-${pkgver}
  python setup.py build_ext --inplace --blosc=/usr
  python setup.py build
}

package_python2-blosc() {
  depends=('python2' 'blosc')

  cd "$srcdir"/blosc-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSES/PYTHON-BLOSC.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-blosc() {
  depends=('python' 'blosc')

  cd "$srcdir"/blosc-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSES/PYTHON-BLOSC.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

