# $Id: PKGBUILD 181150 2016-06-25 07:43:26Z felixonmars $
# Maintainer:

pkgname=aspell-pt
_pkgverPT=20160624
_pkgverBR=20090702
pkgver=$_pkgverPT
pkgrel=1
pkgdesc="Portuguese and Brazilian Portuguese dictionary for aspell"
arch=('i686' 'x86_64')
url="http://aspell.net/"
license=('GPL2')
depends=('aspell')
conflicts=('aspell-pt_br')
replaces=('aspell-pt_br')
source=(ftp://ftp.gnu.org/gnu/aspell/dict/pt_BR/aspell6-pt_BR-$_pkgverBR-0.tar.bz2
        http://natura.di.uminho.pt/download/sources/Dictionaries/aspell6/aspell6.pt-$_pkgverPT.tar.bz2
        http://natura.di.uminho.pt/download/sources/Dictionaries/aspell6/aspell6.pt-preao-$_pkgverPT.tar.bz2)
sha1sums=('add1db9a6a908dccaad13a7fd85c3b202299ff26'
          '615394d836f357108b0fc67e8b205ecd88a09b0a'
          '60ee6fb558b422208cc3c6c3c7d31530cfd5df72')

build() {
  cd aspell6-pt_BR-$_pkgverBR-0
  ./configure
  make

  cd ../aspell6-pt_PT-$_pkgverPT-0
  ./configure
  make

  cd ../aspell6-pt_PT-preao-$_pkgverPT-0
  ./configure
  make
}

package() {
  cd aspell6-pt_BR-$_pkgverBR-0
  make DESTDIR="$pkgdir" install

  cd ../aspell6-pt_PT-$_pkgverPT-0
  make DESTDIR="$pkgdir" install

  cd ../aspell6-pt_PT-preao-$_pkgverPT-0
  make DESTDIR="$pkgdir" install

  echo "add pt_BR.multi" > "$pkgdir"/usr/lib/aspell-0.60/brasileiro.alias
}
