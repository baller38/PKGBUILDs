# $Id: PKGBUILD 141472 2015-09-22 16:00:29Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-hunter
pkgname=('python-hunter' 'python2-hunter')
pkgver=1.3.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Hunter is a flexible code tracing toolkit'
license=('BSD')
url='https://github.com/ionelmc/python-hunter'
makedepends=('python-setuptools' 'python2-setuptools' 'python-fields' 'python2-fields'
             'python-colorama' 'python2-colorama' 'python-six' 'python2-six' 'git'
             'cython' 'cython2')
checkdepends=('python-pytest-benchmark' 'python2-pytest-benchmark' 'python2-virtualenv'
              'python-pytest-runner' 'python2-pytest-runner')
source=("git+https://github.com/ionelmc/python-hunter.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a python-hunter{,-py2}
}

build() {
  cd "$srcdir"/python-hunter
  python setup.py build

  cd "$srcdir"/python-hunter-py2
  python2 setup.py build
}

check() {
  # Hack to process .pth files properly

  cd "$srcdir"/python-hunter
  pyvenv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py install
    python setup.py ptr
  )

  cd "$srcdir"/python-hunter-py2
  virtualenv2 "$srcdir/pyvenv-py2" --system-site-packages
  (
    . "$srcdir/pyvenv-py2/bin/activate"
    python2 setup.py install
    python2 setup.py ptr
  )
}

package_python-hunter() {
  depends=('python-fields' 'python-colorama' 'python-six')

  cd python-hunter
  python setup.py install --root "$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-hunter() {
  depends=('python2-fields' 'python2-colorama' 'python2-six')

  cd python-hunter-py2
  python2 setup.py install --root "$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
