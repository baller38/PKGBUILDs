# $Id: PKGBUILD 177682 2016-06-02 06:28:22Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Zatherz <zatherz at linux dot pl>

pkgname=qt5ct
pkgver=0.24
pkgrel=1
pkgdesc="Qt5 Configuration Utility"
arch=(i686 x86_64)
url="http://opendesktop.org/content/show.php?content=168066"
license=(BSD)
depends=(qt5-svg)
makedepends=(qt5-tools)
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.bz2" 'COPYING.BSD')
sha256sums=('d7acba4e36ed6478a4a27a8cc9359b45f2ecdbcb5f46e6a19e6fdd4252b3c24c'
            'b1742f842de71738501a471e4ce1cac38a02d3240a91848fbd32429bc20aa69b')

build() {
    cd $pkgname-$pkgver
    qmake-qt5 $pkgname.pro 
    make
}

package() {
    cd $pkgname-$pkgver
    make INSTALL_ROOT="$pkgdir" install

    install -Dm644 "$srcdir"/COPYING.BSD "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
} 

