# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-tblib
pkgname=('python-tblib' 'python2-tblib')
_pyname=tblib
pkgver=1.3.0
pkgrel=1
pkgdesc="Python traceback fiddling library"
url="https://github.com/ionelmc/python-tblib"
arch=('any')
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
source=(${pkgbase}-${pkgver}.tar.gz::https://pypi.python.org/packages/source/t/${_pyname}/${_pyname}-${pkgver}.tar.gz)
sha512sums=('2bead497b7a2ff2a23e164935efe5ca242cb6d6063a7d5e1cf88d7d3af6475255435507855f5f359738285d315f0c8c0274c539f33cf5017fcb743c49b8f58a3')

prepare() {
  cp -ra ${_pyname}-${pkgver}{,-py2}
}

check() {
  (cd ${_pyname}-${pkgver}
    python setup.py test
  )
  (cd ${_pyname}-${pkgver}-py2
    python2 setup.py test
  )
}

package_python-tblib() {
  depends=('python-six')
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-tblib() {
  depends=('python2-six')
  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
