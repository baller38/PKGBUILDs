# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux AT free DOT fr>
# Contributor: Juan Diego Tascon
# Contributor: Tom Vincent <http://tlvince.com/contact/>

_gemname=rainbow
pkgname=ruby-${_gemname}
pkgver=2.1.0
pkgrel=1
pkgdesc="Rainbow is extension to ruby's String class adding support for colorizing text on ANSI terminal"
url='https://rubygems.org/gems/rainbow'
arch=('any')
license=('MIT')
depends=('ruby')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sickill/rainbow/archive/v${pkgver}.tar.gz)
sha512sums=('cf3a280301f98c48d9fd52e95c6bcf41fcf4ff9322627ac09a94abb5dd94f56377e62c5d060065c2b97a427d46d5231db74a77aadc4289e43a5ebf3a19777aa1')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|git ls-files|find|g' -i rainbow.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build rainbow.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" rainbow*.gem
  install -Dm 644 README.markdown Changelog.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
