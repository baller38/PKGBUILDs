# $Id: PKGBUILD 175375 2016-05-14 13:41:29Z foutrelis $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=openshadinglanguage
pkgver=1.7.2
pkgrel=2
pkgdesc="Advanced shading language for production GI renderers"
arch=(i686 x86_64)
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'openimageio' 'openexr' 'intel-tbb' 'freetype2' 'libpng'
         'libtiff' 'zlib' 'ncurses')
makedepends=('boost' 'cmake' 'clang35' 'llvm35')
source=(https://github.com/imageworks/OpenShadingLanguage/archive/Release-${pkgver}.tar.gz
        llvm-static-system-libs.patch)
md5sums=('f337bf8b973d38e5cea031c29175d93f'
         'b7c212b2549981ced03c091375f48122')

prepare() {
  cd OpenShadingLanguage-Release-$pkgver

  # Add system libraries needed to link against LLVM components (zlib, ncurses)
  # This is required in order to statically link against LLVM
  patch -Np1 < $srcdir/llvm-static-system-libs.patch
}

build() {
  cd OpenShadingLanguage-Release-$pkgver

  cd src

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake ../.. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLLVM_STATIC=1 \
    -DOSL_BUILD_CPP11=1 \
    -DCMAKE_CXX_FLAGS="\
      -Wno-error=array-bounds \
      -Wno-error=sign-compare \
      -Wno-error=strict-aliasing \
      -Wno-error=misleading-indentation"
  make
}

package() {
  cd OpenShadingLanguage-Release-$pkgver/src/build

  make DESTDIR="$pkgdir/" install

  mkdir -p "$pkgdir"/usr/share/OSL/
  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  mv "$pkgdir"/usr/LICENSE "$pkgdir"/usr/share/licenses/$pkgname # TODO: Tell upstream about this shit
  mv "$pkgdir"/usr/{CHANGES,README.md,INSTALL} "$pkgdir"/usr/share/OSL/
  mv "$pkgdir"/usr/doc "$pkgdir"/usr/share/OSL/doc
  mv "$pkgdir"/usr/shaders "$pkgdir"/usr/share/OSL/shaders
}

# vim:set ts=2 sw=2 et:
