# $Id: PKGBUILD 180686 2016-06-21 08:29:25Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Alain Kalker <a.c.kalker@gmail.com>
# Contributor: Marti Raudsepp <marti@juffo.org>

pkgname=libdwarf
pkgver=20160613
pkgrel=1
pkgdesc="A library for handling DWARF Debugging Information Format"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
url="http://www.prevanders.net/dwarf.html"
depends=('elfutils')
options+=('staticlibs')
source=(http://www.prevanders.net/libdwarf-$pkgver.tar.gz)
sha256sums=('2cd47883a57dd89cb6d32ac36ef14c2631719a4eb2e6e2f743053651416802bb')

build() {
  cd "$srcdir"/dwarf-$pkgver
  ./configure --prefix=/usr --enable-shared
  make basic
}

package() {
  cd "$srcdir"/dwarf-$pkgver/libdwarf

  install -dm755 "$pkgdir"/usr/lib
  install -m644 libdwarf.{a,so.1} "$pkgdir"/usr/lib/
  ln -s libdwarf.so.1 "$pkgdir"/usr/lib/libdwarf.so

  install -dm755 "$pkgdir"/usr/include/libdwarf
  install -m644 dwarf.h libdwarf.h "$pkgdir"/usr/include/libdwarf/

  install -dm755 "$pkgdir"/usr/share/doc/$pkgname
  install -m644 README NEWS *.pdf "$pkgdir"/usr/share/doc/$pkgname/

  # dwarfdump
  cd "$srcdir"/dwarf-$pkgver/dwarfdump
  install -Dm755 dwarfdump "$pkgdir"/usr/bin/dwarfdump
  install -Dm644 dwarfdump.1 "$pkgdir"/usr/share/man/man1/dwarfdump.1
  install -Dm644 dwarfdump.conf "$pkgdir"/usr/lib/dwarfdump.conf
}
