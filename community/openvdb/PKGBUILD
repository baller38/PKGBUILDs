# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

# I would just like to take a minute here to state that this is probably one
# of the worst packaged software packages that I have ever looked at.
# The Makefile has defaults set to the upstream developers' specific systems
# as well as other oddities.

pkgname=openvdb
pkgver=3.1.0
pkgrel=3
pkgdesc='A large suite of tools for the efficient storage and manipulation of sparse volumetric data discretized on three-dimensional grids'
url='https://github.com/dreamworksanimation/openvdb'
arch=('i686' 'x86_64')
license=('MPL')
depends=('openexr' 'boost-libs' 'intel-tbb' 'zlib' 'jemalloc' 'blosc')
makedepends=('doxygen' 'boost' 'python2' 'log4cplus' 'epydoc' 'python2-numpy')
optdepends=('python2-numpy: Python module')
source=("https://github.com/dreamworksanimation/openvdb/archive/v${pkgver}.tar.gz")
sha256sums=('b95a32f4f0195452a64870bda978999a719006a0c036b9ac985b466532d32d4b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/${pkgname}"

  make all \
    PYTHON_VERSION='2.7' \
    CPPUNIT_INCL_DIR='' \
    HDSO='/usr' \
    PYTHON_INCL_DIR=/usr/include/python2.7 \
    PYTHON_LIB_DIR=/usr/lib \
    EXR_LIB_DIR=/usr/lib \
    ILMBASE_LIB_DIR=/usr/lib \
    NUMPY_INCL_DIR=/usr/lib/python2.7/site-packages/numpy/core/include/numpy \
    GLFW_INCL_DIR='' \
    BOOST_PYTHON_LIB_DIR=/usr/lib \
    BOOST_PYTHON_LIB=-lboost_python

}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/${pkgname}"

  mkdir ${pkgdir}/usr
  make install \
    GLFW_INCL_DIR='' \
    DESTDIR=${pkgdir}/usr
}

# vim:set sw=2 sts=2 et:
