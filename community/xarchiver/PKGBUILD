# $Id: PKGBUILD 170767 2016-04-12 15:32:07Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgbase=xarchiver
pkgname=(xarchiver xarchiver-gtk2)
pkgver=0.5.4.6
pkgrel=1
pkgdesc="GTK+ frontend to various command line archivers"
arch=('i686' 'x86_64')
url="https://github.com/ib/xarchiver"
license=('GPL')
depends=('gtk2' 'gtk3' 'desktop-file-utils')
makedepends=('docbook-xsl' 'intltool' 'xmlto')
optdepends=('zip: ZIP support'
            'unzip: ZIP support'
            'p7zip: 7z support'
            'lzop: LZOP support'
            'cpio: RPM support'
            'unrar: RAR support')
install=xarchiver.install
source=(https://github.com/ib/xarchiver/archive/$pkgbase-$pkgver.tar.gz
        0001-Add-support-for-GTK-3.patch
        0001-Fix-docbook-file.patch)
sha256sums=('5199670b435e9a25d33c6101b99f7bd64375377920d389a9a566d9ba72cc9f8b'
            '4ac66b7e23e714ed03fe377b2f8e494c6048a7a507bbc439fe2f081ba110021a'
            '12e4848f81d2532e93ee5bb0774334d176ad09462f07f16a0928558564fdf3a3')

prepare() {
  cd $pkgbase-$pkgbase-$pkgver

  # Add support for GTK+ 3
  patch -Np1 -i ../0001-Add-support-for-GTK-3.patch

  # Fix docbook file
  patch -Np1 -i ../0001-Fix-docbook-file.patch

  ./autogen.sh
}

build() {
  # GTK+ 3 version
  [ -d gtk3 ] || cp -r $pkgbase-$pkgbase-$pkgver gtk3
  cd gtk3
  ./configure --prefix=/usr --libexecdir=/usr/lib/xfce4
  make

  cd "$srcdir"
  # GTK+ 2 version
  [ -d gtk2 ] || cp -r $pkgbase-$pkgbase-$pkgver gtk2
  cd gtk2
  ./configure --prefix=/usr --libexecdir=/usr/lib/xfce4 --disable-gtk3
  make
}

package_xarchiver() {
  depends=('gtk3' 'desktop-file-utils')

  cd gtk3
  make DESTDIR="$pkgdir" install
}

package_xarchiver-gtk2() {
  pkgdesc+=' (GTK+ 2 version)'
  depends=('gtk2' 'desktop-file-utils')
  conflicts=('xarchiver')

  cd gtk2
  make DESTDIR="$pkgdir" install
}
