# $Id: PKGBUILD 178026 2016-06-06 14:07:40Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Mathijs Kadijk <maccain13@gmail.com>

pkgname=(python-dnspython python2-dnspython)
pkgbase=python-dnspython
pkgver=1.14.0
pkgrel=1
pkgdesc="A DNS toolkit for Python"
arch=('any')
url="http://www.dnspython.org"
license=('BSD')
depends=('python')
makedepends=('python-setuptools' 'python2-setuptools')
source=(http://www.dnspython.org/kits/$pkgver/dnspython-$pkgver.tar.gz)
md5sums=('d8971c0c957abceb92c7d36170d07bc6')

check() {
  cd $srcdir/dnspython-$pkgver
  make -C tests check
}

package_python2-dnspython() {
depends=('python2')

  cd $srcdir/dnspython-$pkgver
  python2 setup.py install --root=$pkgdir
  install -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

package_python-dnspython() {
depends=('python')

  cd $srcdir/dnspython-$pkgver
  python setup.py install --root=$pkgdir
  install -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
