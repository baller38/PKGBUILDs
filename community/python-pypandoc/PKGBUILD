# $Id: PKGBUILD 141139 2015-09-20 03:14:59Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pypandoc
pkgname=('python-pypandoc' 'python2-pypandoc')
pkgver=1.1.3
pkgrel=1
pkgdesc="Thin wrapper for pandoc"
arch=('any')
license=('MIT')
url="https://github.com/bebraw/pypandoc"
makedepends=('python-setuptools' 'python2-setuptools' 'pandoc' 'git')
checkdepends=('texlive-core' 'pandoc-citeproc')
source=("git+https://github.com/bebraw/pypandoc.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a pypandoc{,-py2}
}

build() {
  cd "$srcdir/pypandoc"
  python setup.py build

  cd "$srcdir/pypandoc-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/pypandoc"
  python setup.py test

  cd "$srcdir/pypandoc-py2"
  python2 setup.py test
}

package_python-pypandoc() {
  depends=('python' 'pandoc')

  cd pypandoc
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pypandoc() {
  depends=('python2' 'pandoc')

  cd pypandoc-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
