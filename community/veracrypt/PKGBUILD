# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sebastian Lau <archlinux _at_ slau _dot_ info>
# Contributor: Eric Ozwigh <ozwigh at gmail dot com>

pkgname=veracrypt
_pkgname=VeraCrypt
pkgver=1.17
pkgrel=5
pkgdesc='Disk encryption with strong security based on TrueCrypt'
url='http://veracrypt.codeplex.com/'
arch=('i686' 'x86_64')
license=('custom:TrueCrypt')
depends=('fuse>=2.8.0' 'wxgtk>=3.0' 'libsm' 'device-mapper')
optdepends=('sudo: mounting encrypted volumes as nonroot users')
makedepends=('nasm')
source=("${pkgname}_${pkgver}_Source.tar.bz2::https://sourceforge.net/projects/${pkgname}/files/${_pkgname} ${pkgver}/${pkgname}_${pkgver}_Source.tar.bz2/download"
        "${pkgname}_${pkgver}_Source.tar.bz2.sig::https://sourceforge.net/projects/${pkgname}/files/${_pkgname} ${pkgver}/${pkgname}_${pkgver}_Source.tar.bz2.sig/download"
        no-makeself.patch
        truecrypt-arch-detection.patch
        gcc6.patch
        fix-invalid-characters.patch
        no-exec-stack.patch
        veracrypt.desktop)
sha512sums=('a26c28513030b43390942f5363c3e501ef36ac8277a9f10cf8339a0ba487fea4d0db853c96f7f2540b0b2ff417e182525b1ad193f4ef6e5078224bc466236ee9'
            '82e09ff9132b20dff75d509be3da43a9aa1b23d14e52f35d248d1e578360f6bb191a328332cd503c324a01fe739ef301b4c532dc7398861d2b094c6a7fc76825'
            'e3972fd239fd83807c5ea9dd9268a0e962b0bd9cb9cf8f6251ac51a44a94f3c57f0b1e6857b42e4296cff531030c8278c543b386ebf463d4e19fe9a536acbcf9'
            'fac6963c65720f0b13486ff22bdcc2e31e78c39ede2b9561b2c4037ef2c0375f04c4d51dc6067c37aaeb664c3ed70e5747488389737c41807680750c26a8f084'
            '84175a3677f20b2b03ab29b017b9deecb517a58a640cf667b503cc9375c104b9c0a5d5ea4b3537ba7f12530054a456b466df0afdd935714cd43034b8e6cb2b7c'
            'd52826572178a471bb608fd4991766f4f9fa7c5cbf3659922372feb9fdfcbab596d5f1360e30f66613681619672d0be6c5457f5617f5fb1f84061de779831f96'
            'c64f9255303a521b4e531ebea574befe80a9f193c9aa42fd9cb552e56d087815ca161b50b593e7c3ede10a65c67dc36d0447dbffb0f4d4614f181a95759c2f79'
            'f689ca64bac7042030de7714aed8cc89f2c5f87b407444b1b121491c1d89c147babaaa454ddc2a93b70ae20d4da59f96ad64f01b04bea9017d658c377faeb75d')
validpgpkeys=('993B7D7E8E413809828F0F29EB559C7C54DDD393') # VeraCrypt Team <veracrypt@idrix.fr>

prepare() {
  cd src
  patch -p1 < "${srcdir}/no-makeself.patch"
  patch -p1 < "${srcdir}/truecrypt-arch-detection.patch"
  patch -p0 < "${srcdir}/gcc6.patch"
  patch -p2 < "${srcdir}/fix-invalid-characters.patch"
  patch -p0 < "${srcdir}/no-exec-stack.patch"
}

build() {
  cd src
  export WX_CONFIG=/usr/bin/wx-config
  make TC_EXTRA_LFLAGS+="-ldl ${LDFLAGS}" \
    TC_EXTRA_CXXFLAGS="${CXXFLAGS}" \
    TC_EXTRA_CFLAGS="${CFLAGS}"
}

package() {
  cd src
  install -Dm 755 Main/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 "${srcdir}/veracrypt.desktop" -t "${pkgdir}/usr/share/applications"
  install -Dm 644 Resources/Icons/VeraCrypt-256x256.xpm "${pkgdir}/usr/share/pixmaps/veracrypt.xpm"
  install -Dm 644 License.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
