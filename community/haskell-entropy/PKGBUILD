# $Id: PKGBUILD 179320 2016-06-11 06:30:08Z fyan $
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Daniel Nagy <danielnagy at gmx de>

pkgname=haskell-entropy
pkgver=0.3.7
pkgrel=2
pkgdesc="A platform independent method to obtain cryptographically strong entropy"
url="http://hackage.haskell.org/package/entropy"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('ghc')
source=(http://hackage.haskell.org/packages/archive/entropy/${pkgver}/entropy-${pkgver}.tar.gz)
md5sums=('2ac059eebfb0d2368df1b825207055c2')

build() {
    cd ${srcdir}/entropy-${pkgver}
    runhaskell Setup configure -O --enable-library-profiling --enable-split-objs --enable-shared \
       --prefix=/usr --docdir=/usr/share/doc/${pkgname} --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/entropy-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/entropy
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
