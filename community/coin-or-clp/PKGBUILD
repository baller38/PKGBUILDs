# $Id: PKGBUILD 169508 2016-04-05 06:49:03Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

pkgname=coin-or-clp
pkgver=1.16.10
pkgrel=4
pkgdesc="COIN-OR linear programming solver"
arch=(i686 x86_64)
url="https://projects.coin-or.org/Clp"
license=(EPL)
groups=(coin-or)
depends=(coin-or-osi suitesparse)
source=("http://www.coin-or.org/download/source/Clp/Clp-${pkgver}.tgz")
sha1sums=('c74ef781ef050df6a127186f7a0069118cf89b12')

build() {
  cd Clp-$pkgver/Clp
  COIN_SKIP_PROJECTS="Sample" \
  ./configure --prefix=/usr \
              --with-osi-lib="$(pkg-config --libs osi)" \
              --with-osi-incdir="/usr/include/coin/" \
              --with-coinutils-lib="$(pkg-config --libs coinutils)" \
              --with-coinutils-incdir="/usr/include/coin/" \
              --with-cholmod-lib='-lcholmod' --with-cholmod-incdir=/usr/include
  make
}

check() {
  cd Clp-$pkgver/Clp
# make test
}

package() {
  cd Clp-$pkgver/Clp
  PKG_CONFIG_LIBDIR="$pkgdir"/usr/lib/pkgconfig/ \
  make DESTDIR="$pkgdir" install
}
