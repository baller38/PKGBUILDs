# $Id: PKGBUILD 176869 2016-05-25 11:36:56Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=shotwell
epoch=1
pkgver=0.23.1
_basever=$(echo $pkgver | cut -f1-2 -d.)
pkgrel=1
pkgdesc="A digital photo organizer designed for the GNOME desktop environment"
arch=('i686' 'x86_64')
url="http://yorba.org/shotwell/"
license=('LGPL2.1')
depends=('libgee' 'webkit2gtk' 'libgexiv2' 'libraw' 'rest'
	 'desktop-file-utils' 'dconf' 'hicolor-icon-theme' 'json-glib'
	 'gst-plugins-base' 'libgphoto2' 'libgudev')
makedepends=('intltool' 'vala>=0.17.2' 'gnome-doc-utils')
source=("https://download.gnome.org/sources/shotwell/${_basever}/shotwell-$pkgver.tar.xz"
        "webkit2.patch")
md5sums=('2177d900fd32e4be474b346bf4b9fe16'
         '2c2c96d15c810c96a2cd36f8fcab197d')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Port to webkit2gtk
#  patch -Np1 -i ../webkit2.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --libexec=/usr/lib/shotwell \
	--disable-schemas-compile \
	--disable-desktop-update \
	--disable-icon-update
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
