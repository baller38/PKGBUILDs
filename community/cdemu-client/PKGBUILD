# $Id: PKGBUILD 155737 2016-01-08 23:27:47Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=cdemu-client
pkgver=3.0.1
pkgrel=2
pkgdesc="Simple command-line client for controlling cdemu-daemon"
arch=(any)
url="http://cdemu.sourceforge.net/"
license=(GPL)
depends=(python2 dbus-python cdemu-daemon desktop-file-utils)
makedepends=(intltool cmake)
conflicts=('cdemu')
install=cdemu-client.install
source=("http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.bz2")
sha256sums=('cf7b2fedf14bb64df7e318546756e133eef9e018e04218f74891f676eda9e5cd')

prepare() {
  mkdir build
  cd $pkgname-$pkgver

  # python2 fix
  sed -i '1s|^#!.*python$|&2|' src/cdemu

  # fix text domain
  sed -i '/^project /s/cdemu/&-client/' CMakeLists.txt

  # bash completions
  sed -e 's|${CMAKE_INSTALL_SYSCONFDIR}/bash_completion.d|${CMAKE_INSTALL_DATADIR}/bash-completion/completions|g' \
      -e '/FILES data\/cdemu-bash-completion.sh/aRENAME cdemu' \
      -i CMakeLists.txt
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
