# $Id: PKGBUILD 180805 2016-06-23 11:17:10Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Brian Nguyen <bhnguyen@gmail.com>
# Original Packager: Tom K <tomk@runbox.com>

pkgname=hevea
pkgver=2.28
pkgrel=1
pkgdesc="A quite complete and fast LATEX to HTML translator"
arch=(i686 x86_64)
depends=('ocaml' 'texlive-bin')
license=('GPL')
url="http://pauillac.inria.fr/~maranget/hevea/"
install=hevea.install
source=(http://pauillac.inria.fr/~maranget/$pkgname/distri/$pkgname-$pkgver.tar.gz)
md5sums=('7164a099279739cdef9037612d4a134c')

build() {
  cd $srcdir/$pkgname-$pkgver
  sed -i 's:usr/local:usr:' Makefile
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  sed -i 's:/usr:$PREFIX:' config.sh
  make install PREFIX=$pkgdir/usr
  mkdir -p $pkgdir/usr/share/texmf/tex/latex
  ln -s /usr/lib/hevea/hevea.sty $pkgdir/usr/share/texmf/tex/latex/hevea.sty
}
