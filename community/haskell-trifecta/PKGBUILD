# $Id: PKGBUILD 181115 2016-06-25 02:37:55Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=trifecta
pkgname=haskell-trifecta
pkgver=1.5.2
pkgrel=23
pkgdesc="A modern parser combinator library with convenient diagnostics"
url="http://github.com/ekmett/trifecta/"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=("ghc=8.0.1" "haskell-ansi-terminal" "haskell-ansi-wl-pprint" "haskell-blaze-builder"
         "haskell-blaze-html" "haskell-blaze-markup" "haskell-charset" "haskell-comonad"
         "haskell-fingertree" "haskell-hashable" "haskell-lens" "haskell-mtl" "haskell-parsers"
         "haskell-profunctors" "haskell-reducers" "haskell-semigroups" "haskell-unordered-containers"
         "haskell-utf8-string")
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('ebc23319d2cfd39c87b2cf688f3db45528e371de9a417fc9e07e50a796b6503a')

prepare() {
    sed -e 's/comonad              >= 4       && < 5,/comonad              >= 4       \&\& < 6,/' \
        -e 's/transformers         >= 0.2     && < 0.5,/transformers         >= 0.2     \&\& < 0.6,/' \
        -i ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
