# $Id: PKGBUILD 163522 2016-02-26 19:36:44Z arcanis $
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot ord>
# Contributor: Andrejs Mivre≈Üiks <gim at fastmail dot fm>

pkgname=libasl
pkgver=0.1.6
pkgrel=5
pkgdesc='Multiphysics simulation software package (Advanced Simulation Library)'
arch=('i686' 'x86_64')
url='http://asl.org.il/'
license=('AGPL3')
depends=('boost-libs' 'libcl' 'libmatio' 'vtk' 'libjpeg' 'libpng' 'libtiff'
         'freetype2' 'libgl')
makedepends=('cmake' 'opencl-headers' 'boost' 'mesa-libgl')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/AvtechScientific/ASL/archive/v${pkgver}.tar.gz"
        'suppress-deprecated-warnings.patch'
        'libasl-vtk7.patch'
        'libasl-0.1.6-Explicitly-define-namespace-of-ifstream-and-ofstream.patch')
md5sums=('1f283d066d57e11d4602016148181420'
         '972d7ed8037a35d32125773ae70f6360'
         '583fff4d0e81579720e094ecb8729c2d'
         '4747d00c048574f5722b4d9cfaa73676')

prepare() {
  rm -rf "build"
  mkdir "build"

  # patch to suppress 'deprecated' warning messages
  # caused by 'opencl-headers' since update to 2.0
  cd "ASL-${pkgver}"
  patch -p1 -i "${srcdir}/suppress-deprecated-warnings.patch"

  # patch cmakelists for newest vtk
  patch -p1 -i "${srcdir}/libasl-vtk7.patch"

  patch -p1 -i "${srcdir}/libasl-0.1.6-Explicitly-define-namespace-of-ifstream-and-ofstream.patch"
}

build() {
  cd "build"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DBUILD_SHARED_LIBS=ON \
        -DWITH_EXAMPLES=ON \
        -DWITH_MATIO=ON \
        -DWITH_API_DOC=OFF \
        -DWITH_TESTS=OFF \
        "../ASL-${pkgver}"
  make
}

package() {
  cd "build"
  make DESTDIR="${pkgdir}" install
}

