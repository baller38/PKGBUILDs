# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Ã˜yvind 'Mr.Elendig' Heggstad <mrelendig@har-ikkje.net>

pkgname=haskell-setlocale
_hkgname=setlocale
pkgver=1.0.0.3
pkgrel=3
pkgdesc="Haskell bindings to setlocale"
url="http://hackage.haskell.org/package/setlocale"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('ghc')
source=(http://hackage.haskell.org/packages/archive/setlocale/${pkgver}/setlocale-${pkgver}.tar.gz)
md5sums=('348690842f5233f0e42e16bd615f777e')

prepare() {
    sed -i 's/base >=4.6 && <4.9/base >=4.6 \&\& <4.10/' setlocale-${pkgver}/setlocale.cabal
}

build() {
    cd ${srcdir}/setlocale-${pkgver}
    runhaskell Setup configure -O --enable-library-profiling --enable-split-objs --enable-shared \
       --prefix=/usr --docdir=/usr/share/doc/${pkgname} --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/setlocale-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/$pkgname/html "$pkgdir/usr/share/doc/ghc/html/libraries/$_hkgname"

    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
