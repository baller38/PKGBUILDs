# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jacques Fontaine <waitnsea@free.fr>
# Contributor: Ryan Young <ryan31415926535@gmail.com>

pkgname=linssid
pkgver=2.7
pkgrel=4
pkgdesc='Graphical wireless scanner for Linux'
url='https://sourceforge.net/projects/linssid/'
arch=('i686' 'x86_64')
license=('GPL3')
depends=('qt5-base' 'qt5-svg' 'libxkbcommon-x11' 'iw' 'wireless_tools' 'xdg-utils' 'desktop-file-utils' 'gtk-update-icon-cache')
makedepends=('boost')
optdepends=('sudo: privilege escalation to use wireless tools')
source=(${pkgname}-${pkgver}.tar.gz::https://downloads.sourceforge.net/${pkgname}/${pkgname}_$pkgver.orig.tar.gz)
sha512sums=('33e69b0491bee74453cb71befb3a314da7766f12d2c1a21e947cf1d068b2941162eb72ab53989d9e5e5e2c8f2dd051576d0bfb775835d648eb9b1fc8b046a0cb')

prepare() {
  cd ${pkgname}-${pkgver}
  # build workarounds, credit 'czerwonyd'
  sed -e 's/QT_STATIC_CONST/static const/g' -i qwt-lib/src/qwt_transform.h
  sed -e 's/QT_STATIC_CONST_IMPL/const/g' -i qwt-lib/src/qwt_transform.cpp
}
build() {
  cd ${pkgname}-${pkgver}
  qmake-qt5
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make INSTALL_ROOT="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
