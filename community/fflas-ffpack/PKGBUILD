# $Id: PKGBUILD 175851 2016-05-20 09:57:48Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=fflas-ffpack
pkgver=2.2.1
pkgrel=7
pkgdesc="A library for dense linear algebra over word-size finite fields"
arch=(i686 x86_64) # pkgconfig file is arch-specific
url="http://linbox-team.github.io/fflas-ffpack/"
license=(LGPL)
makedepends=(givaro cblas lapack)
source=("https://github.com/linbox-team/fflas-ffpack/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('4aa8d77f233774ed29c4be84e43190dd')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-blas-libs="-lcblas -llapack -lgomp" --disable-avx --disable-sse
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
