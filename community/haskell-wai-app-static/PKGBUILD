# $Id: PKGBUILD 181338 2016-06-27 07:09:30Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=wai-app-static
pkgname=haskell-wai-app-static
pkgver=3.1.5
pkgrel=19
pkgdesc="WAI application for static serving"
url="http://www.yesodweb.com/book/web-application-interface"
license=("MIT")
arch=('i686' 'x86_64')
depends=("ghc=8.0.1" "haskell-blaze-builder" "haskell-blaze-html" "haskell-blaze-markup" 
         "haskell-cryptonite" "haskell-file-embed" "haskell-http-date" "haskell-http-types"
         "haskell-memory" "haskell-mime-types" "haskell-old-locale"
         "haskell-optparse-applicative" "haskell-text" "haskell-unix-compat"
         "haskell-unordered-containers" "haskell-wai" "haskell-wai-extra" "haskell-warp"
         "haskell-zlib")
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('28667193acfcc534752b715b5f5e16fc58edb550d03c0eb2b68e123e41030d4c')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -f-print
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
