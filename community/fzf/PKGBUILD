# Maintainer: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: foalsrock <foalsrock at gmail dot-com>
# Contributor: jebaum <jebaum at ucla dot edu>

pkgname=fzf
pkgver=0.13.2
pkgrel=1
pkgdesc="Command-line fuzzy finder"
arch=("i686" "x86_64")
url="https://github.com/junegunn/fzf"
license=("MIT")
depends=("ncurses")
makedepends=("git" "go")
optdepends=("fish: fish keybindings"
	"fzf-extras: fzf keybindings from junegunn wiki and dotfiles"
	"tmux: fzf-tmux script for launching fzf in a tmux pane"
	"vim-fzf: vim plugin"
	"zsh: zsh keybindings")
install="fzf.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/junegunn/fzf/archive/$pkgver.tar.gz"
	"git+https://github.com/junegunn/$pkgname.wiki"
	"key-bindings.bash.patch"
	"key-bindings.zsh.patch")
sha256sums=('063c2e0e23944acead08e90a33ebce0d5d1c05b168571f56800b3b2ddf7c5ee9'
            'SKIP'
            'fc981d036d85d0b9a5e6ba65f84b1c0f86bcee2dabb09238e1edfa1f49a85b63'
            '0b89bf1d6d372b9a8d5927e58151157d42848086a252979006b89b72242c52e6')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"

	msg2 "Patching bash and zsh key bindings..."
	patch -p1 < "$srcdir/key-bindings.bash.patch"
	patch -p1 < "$srcdir/key-bindings.zsh.patch"
}

build() {
	cd "$srcdir/$pkgname-$pkgver"

	msg2 'Building...'
	GOPATH="$srcdir" TMPDIR=/tmp go get -u github.com/junegunn/fzf/...
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	## Doc
	install -dm755 "$pkgdir/usr/share/doc/fzf/wiki"
	install -m644 "$srcdir/fzf.wiki"/* "$pkgdir/usr/share/doc/fzf/wiki"
	install -m644 README.md install uninstall "$pkgdir/usr/share/doc/fzf"

	## Man page
	install -Dm644 "man/man1/fzf.1" "$pkgdir/usr/share/man/man1/fzf.1"

	## License
	install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/fzf/LICENSE"

	## Binaries
	install -dm755 "$pkgdir/usr/bin"
	install -m755 "$srcdir/bin/fzf" "bin/fzf-tmux" "$pkgdir/usr/bin/"

	## Bash completion and keybindings
	install -Dm644 "shell/completion.bash" "$pkgdir/usr/share/bash-completion/completions/fzf"
	install -Dm755 "shell/key-bindings.bash" "$pkgdir/usr/share/fzf/key-bindings.bash"

	## Fish keybindings
	install -Dm644 "shell/key-bindings.fish" "$pkgdir/usr/share/fish/functions/fzf.fish"

	## Zsh completion and keybindings
	install -Dm644 "shell/completion.zsh" "$pkgdir/usr/share/zsh/site-functions/_fzf"
	install -Dm755 "shell/key-bindings.zsh" "$pkgdir/usr/share/fzf/key-bindings.zsh"
}
