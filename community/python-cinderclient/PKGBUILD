# $Id: PKGBUILD 177616 2016-06-01 16:18:41Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-cinderclient
pkgname=(python-cinderclient python2-cinderclient)
pkgver=1.8.0
pkgrel=1
pkgdesc="OpenStack Block Storage API Client Library"
arch=('any')
url="http://docs.openstack.org/developer/python-cinderclient"
license=('Apache')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-babel' 'python2-babel' 'python-prettytable' 'python2-prettytable'
             'python-keystoneclient' 'python2-keystoneclient' 'python-requests'
             'python2-requests' 'python-oslo-utils' 'python2-oslo-utils')
checkdepends=('python-oslotest' 'python2-oslotest' 'python-requests-mock'
              'python2-requests-mock' 'python-ddt' 'python2-ddt')
source=("git+https://git.openstack.org/openstack/python-cinderclient#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  sed -i '/simplejson/d' python-cinderclient/requirements.txt
  cp -a python-cinderclient{,-py2}
}

build() {
  cd "$srcdir"/python-cinderclient
  python setup.py build

  cd "$srcdir"/python-cinderclient-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-cinderclient
  python setup.py testr

  cd "$srcdir"/python-cinderclient-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-cinderclient() {
  depends=('python-six' 'python-pbr' 'python-babel' 'python-prettytable' 'python-keystoneclient'
           'python-requests' 'python-oslo-utils')

  cd "$srcdir"/python-cinderclient
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-cinderclient() {
  depends=('python2-six' 'python2-pbr' 'python2-babel' 'python2-prettytable' 'python2-keystoneclient'
           'python2-requests' 'python2-oslo-utils')

  cd "$srcdir"/python-cinderclient-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1

  mv "$pkgdir"/usr/bin/cinder{,2}
}

# vim:set ts=2 sw=2 et:
