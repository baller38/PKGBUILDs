# $Id: PKGBUILD 68685 2012-03-31 16:54:07Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Babets <fbabetz@yahoo.it>
# Contributor: Leslie P. Polzer <polzer@stardawn.org>

pkgname=minizip
pkgver=1.1
pkgrel=1
epoch=1
pkgdesc='ZIP file extraction library'
url='https://github.com/nmoinvaz/minizip'
license=('ZLIB' 'custom')
arch=('x86_64' 'i686')
makedepends=('git')
depends=('zlib')
source=("git://github.com/nmoinvaz/minizip.git#commit=c9e5b68")
md5sums=('SKIP')

prepare() {
  cd "$pkgname"

  rm -f Makefile
  autoreconf -i
  autoconf && automake --add-missing && automake
}

build() {
  cd "$pkgname"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname"
  make install DESTDIR="$pkgdir"
  libtool --finish "$pkgdir/usr/lib"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: raw.githubusercontent.com/nmoinvaz/minizip/master/unzip.h
# vim:set ts=2 sw=2 et:
