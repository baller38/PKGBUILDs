# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Contributor: Mark Pustjens <pustjens@dds.nl>

pkgname=xpdf
pkgver=3.04
pkgrel=5
pkgdesc='Viewer for Portable Document Format (PDF) files'
url='http://www.foolabs.com/xpdf/'
arch=('i686' 'x86_64')
license=('GPL2')
backup=('etc/xpdfrc')
depends=('lesstif' 'gsfonts' 'libxt' 'libpaper')
optdepends=('poppler: tools that used to be included in xpdf'
            'desktop-file-utils: for desktop environments')
source=(ftp://ftp.foolabs.com/pub/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig}
        02-permissions.patch
        xpdf.desktop)
sha512sums=('3fb3c380fdbd13d937449ae3f7d7a163815cc85296d30be7b9907687884385d6171bb48ee5d5c13368d6bba87740b678f491bdcf61b38d130809e9afa260f6cf'
            'SKIP'
            'a5d9358b02e48a49db8d07ecbad67d03aef2614e9bbee7207074a55deb9358a97289b52b04bb6e78f5c99be3d4858abf35c1a42c2e91dfa86c7f382d631dc93c'
            'd6163eb220bd567226c6006bef30c6db1eac1263ae161392db9766dca1ce78014cc555dd9d27febd2b924150053fa01b47c06777b1e760864459574c7614bfc4')
validpgpkeys=('22CCDF00E35F018434DCD4C5429CCA86671B682D') # Derek B. Noonburg <derekn@foolabs.com>

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < "${srcdir}/02-permissions.patch"
  sed -i 's:/usr/share/fonts/type1/gsfonts:/usr/share/fonts/Type1:' xpdf/GlobalParams.cc
  sed -i 's:times-medium-r-normal--16:times-medium-r-normal--14:' xpdf/XPDFViewer.cc # FS#14217
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --mandir=/usr/share/man \
    --enable-multithreaded \
    --with-freetype2-library=/usr/lib \
    --with-freetype2-includes=/usr/include/freetype2 \
    --x-includes=/usr/include \
    --with-Xm-library=/usr/lib \
    --with-Xm-includes=/usr/include
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -Dm 644 "${srcdir}/xpdf.desktop" -t "${pkgdir}/usr/share/applications"
  install -Dm 644 xpdf/xpdfIcon.xpm "${pkgdir}/usr/share/pixmaps/xpdf.xpm"

  # stuff provided by poppler
  for tool in pdfdetach pdffonts pdfimages pdfinfo pdftoppm pdftops pdftotext; do
    rm "${pkgdir}/usr/bin/${tool}" "${pkgdir}/usr/share/man/man1/${tool}.1"
  done
}
