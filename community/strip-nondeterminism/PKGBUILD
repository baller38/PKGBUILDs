# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=strip-nondeterminism
pkgver=0.019
pkgrel=1
pkgdesc='Tool for stripping bits of non-deterministic information from files'
url='https://anonscm.debian.org/git/reproducible/strip-nondeterminism.git'
arch=('any')
license=('GPL3')
depends=("perl" 'perl-archive-zip')
makedepends=('git')
options=('!emptydirs')
source=(${pkgname}::"git+https://anonscm.debian.org/git/reproducible/strip-nondeterminism.git#tag=${pkgver}")
sha512sums=('SKIP')
validpgpkeys=('C2FE4BD271C139B86C533E461E953E27D4311E58'  # Chris Lamb <lamby@debian.org>
              'EF5D84C1838F2EB6D8968C0410378EFC2080080C'  # Andrew Ayer <agwa@andrewayer.name>
              '2F5DAF3FC1F793D94F3D900CA721DA055374AA4F'  # Reiner Herrmann <reiner@reiner-h.de>
              '66AE2B4AFCCF3F52DA184D184B043FCDB9444540') # Mattia Rizzolo <mattia@debian.org>

prepare() {
  cd ${pkgname}
  git tag --verify ${pkgver}
  sed -r 's|bin/dh_strip_nondeterminism ||g' -i Makefile.PL
}

build() {
  cd ${pkgname}
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:
