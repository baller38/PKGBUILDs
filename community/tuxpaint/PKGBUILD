# $Id: PKGBUILD 155743 2016-01-09 02:24:33Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Sergio Jovani Guzman <moret.sjg@gmail.com>
# Contributor: royrocks <royrocks13@gmail.com>

pkgname=tuxpaint
pkgver=0.9.22
pkgrel=2
pkgdesc='Drawing program designed for young children'
arch=('x86_64' 'i686')
url='http://www.tuxpaint.org/'
license=('GPL')
backup=('etc/tuxpaint/tuxpaint.conf')
depends=('python2' 'fribidi' 'sdl_ttf' 'sdl_mixer' 'sdl_image' 'sdl_pango'
         'libpaper' 'librsvg')
makedepends=('setconf' 'optipng' 'gperf')
optdepends=('tuxpaint-stamps: more stamps'
            'tuxpaint-conf: configuration manager')
source=("http://downloads.sourceforge.net/sourceforge/tuxpaint/$pkgname-$pkgver.tar.gz")
sha256sums=('8abd932f8e36fad795bba0290c094c9c221e424c4b9aa8c763b7fb3459cb2be3')

prepare() {
  cd "$pkgname-$pkgver"

  setconf src/tuxpaint.desktop Categories \
    'Game;KidsGame;Graphics;RasterGraphics;'

  # fullscreen
  sed -i 's:# fu:fu:' src/tuxpaint.conf

  # python2 fix
  for f in docs/zh_tw/mkTuxpaintIM.py fonts/locale/zh_tw_docs/maketuxfont.py; do
    sed -i '0,/on/s//on2/' $f
  done

  # libpng15 fix
  sed -i 's:$(ARCH_LINKS):$(ARCH_LINKS) -lpng:' Makefile

  # libpng16 fix
  find -name '*.png' -exec optipng -quiet -force -fix {} +

  # bash completion placement
  setconf Makefile COMPLETIONDIR '$(DESTDIR)/usr/share/bash-completion/completions'
}

build() {
  cd "$pkgname-$pkgver"
  make clean
  make translations -j1
  make PREFIX=/usr -j1
}

package() {
  ls "$pkgname-$pkgver/trans"
  make -j1 -C "$pkgname-$pkgver" \
    PREFIX=/usr \
    DESTDIR="$pkgdir" \
    GNOME_PREFIX=/usr \
    X11_ICON_PREFIX="$pkgdir/usr/share/pixmaps" \
    install
  chmod -R 644 "$pkgdir/usr/share/doc/tuxpaint-dev/"*
  chmod 755 "$pkgdir/usr/share/doc/tuxpaint-dev/html"
  mv "$pkgdir/usr/share/bash-completion/completions/tuxpaint-completion.bash" \
    "$pkgdir/usr/share/bash-completion/completions/tuxpaint"
}

# getver: tuxpaint.org/download/source
# vim:set ts=2 sw=2 et:
