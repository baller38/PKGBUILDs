# $Id: PKGBUILD 181342 2016-06-27 07:09:34Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=mono-traversable
pkgname=haskell-mono-traversable
pkgver=0.10.2
pkgrel=11
pkgdesc="Type classes for mapping, folding, and traversing monomorphic containers"
url="https://github.com/snoyberg/mono-traversable"
license=("MIT")
arch=('i686' 'x86_64')
depends=("ghc=8.0.1" "haskell-comonad" "haskell-dlist" "haskell-dlist-instances"
         "haskell-hashable" "haskell-semigroupoids" "haskell-semigroups"
         "haskell-split" "haskell-text" "haskell-unordered-containers"
         "haskell-vector" "haskell-vector-algorithms" "haskell-vector-instances")
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('379ee5a7f9fc2a5c4fb11522fe28654d130c044265643122c8b3163e8e0452b8')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
