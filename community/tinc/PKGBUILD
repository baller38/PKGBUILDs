# $Id: PKGBUILD 175329 2016-05-14 10:55:19Z arojas $
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: helios
# Contributor: Lothar Gesslein
# Contributor: Dominik George <nik@naturalnet.de>

pkgname=tinc
pkgver=1.0.28
pkgrel=2
pkgdesc="VPN (Virtual Private Network) daemon"
arch=('i686' 'x86_64')
url="http://www.tinc-vpn.org/"
license=('GPL')
depends=('lzo' 'openssl' 'zlib')
source=(http://www.tinc-vpn.org/packages/tinc-$pkgver.tar.gz{,.sig})
md5sums=('9ca14c9902fb4011b5df6a09ffd40ea9'
         'SKIP')
validpgpkeys=('D62BDD168EFBE48BC60E8E234A6084B9C0D71F4A')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --sbindir=/usr/bin --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -dm755 "$pkgdir"/usr/share/tinc/examples
  tar xozvf doc/sample-config.tar.gz -C $pkgdir/usr/share/tinc/examples
  find "$pkgdir"/usr/share/tinc/examples -type f -exec chmod 644 {} +
  find "$pkgdir"/usr/share/tinc/examples -type d -exec chmod 755 {} +
}
