# Contributor: RÃ©my Oudompheng <oudomphe@phare.normalesup.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=gmp-ecm
pkgver=7.0
pkgrel=1
pkgdesc='Elliptic Curve Method for Integer Factorization'
url='http://ecm.gforge.inria.fr/'
license=('GPL' 'LGPL')
depends=('gmp')
options=('!libtool')
arch=('i686' 'x86_64')
source=("http://gforge.inria.fr/frs/download.php/35642/ecm-${pkgver}.tar.gz")
sha1sums=('b5acb139099f197d4c38aae079543ba26fa20737')

build() {
	cd "${srcdir}/ecm-${pkgver}"
	[[ $CARCH = *86* ]] && export CFLAGS+=' -msse'
	./configure --prefix=/usr --enable-shared --enable-openmp
	# make ecm-params
	make
}

check() {
	cd "${srcdir}/ecm-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/ecm-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
