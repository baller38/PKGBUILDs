# $Id: PKGBUILD 163505 2016-02-26 14:54:44Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=lxde-common
pkgver=0.99.1
pkgrel=2
pkgdesc='Common files of the LXDE Desktop'
arch=('any')
url='http://lxde.org/'
license=('GPL2')
groups=('lxde' 'lxde-gtk3')
backup=(etc/xdg/lxsession/LXDE/desktop.conf
        etc/xdg/lxsession/LXDE/autostart)
makedepends=('intltool')
optdepends=('gnome-themes-standard: default GTK+ theme'
            'lxde-icon-theme: default icon theme')
source=(http://downloads.sourceforge.net/lxde/$pkgname-$pkgver.tar.xz
        dbus-update-environment.patch
        default-config.patch)
md5sums=('3ab92bd7bcff8bc8e6395651d164cf76'
         '9f5cdfb8aef6286b6de79a50d1fddd7f'
         'fc8a3499ee9caeff1fbdeb1761fe8a4e')

prepare() {
    cd $pkgname-$pkgver
    patch -Np1 -i ../dbus-update-environment.patch
    patch -Np1 -i ../default-config.patch
}

build() {
    cd $pkgname-$pkgver
    ./configure --sysconfdir=/etc --prefix=/usr
    make
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
