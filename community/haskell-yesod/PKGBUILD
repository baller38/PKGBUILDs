# $Id: PKGBUILD 181397 2016-06-27 10:57:34Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=yesod
pkgname=haskell-yesod
pkgver=1.4.3
pkgrel=21
pkgdesc="Creation of type-safe, RESTful web applications."
url="http://www.yesodweb.com/"
license=("MIT")
arch=('i686' 'x86_64')
depends=("ghc=8.0.1" "haskell-aeson" "haskell-blaze-html" "haskell-blaze-markup"
         "haskell-conduit-extra" "haskell-data-default" "haskell-fast-logger"
         "haskell-monad-control" "haskell-monad-logger" "haskell-safe"
         "haskell-semigroups" "haskell-shakespeare" "haskell-streaming-commons"
         "haskell-text" "haskell-unordered-containers" "haskell-wai"
         "haskell-wai-extra" "haskell-wai-logger" "haskell-warp" "haskell-yaml"
         "haskell-yesod-auth" "haskell-yesod-core" "haskell-yesod-form"
         "haskell-yesod-persistent")
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('13655ed28102b30f32a34fb1b30cf20c1d9bbd9f6f1c89f96643ea6d7bba74a3')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
