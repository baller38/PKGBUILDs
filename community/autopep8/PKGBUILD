# $Id: PKGBUILD 175982 2016-05-21 04:50:05Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ismael Carnales <icarnales at gmail dot com>
# Contributor: Sergey Mastykov <smastykov[at]gmail[dot]com>

pkgbase=autopep8
pkgname=(autopep8 python2-autopep8)
pkgver=1.2.4
pkgrel=1
pkgdesc="A tool that automatically formats Python code to conform to the PEP 8 style guide"
arch=('any')
url="http://github.com/hhatto/autopep8"
license=('MIT')
makedepends=('pep8' 'python2-pep8' 'git')
source=("git+https://github.com/hhatto/autopep8.git#tag=v$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a autopep8{,-py2}
}

build() {
  cd "$srcdir"/autopep8
  python setup.py build

  cd "$srcdir"/autopep8-py2
  python2 setup.py build
}

check() {
  export LC_CTYPE=en_US.UTF-8

  cd "$srcdir"/autopep8
  python setup.py test

  cd "$srcdir"/autopep8-py2
  python2 setup.py test
}

package_autopep8() {
  depends=('pep8')

  cd autopep8
  python3 setup.py install -O1 --prefix=/usr --root="$pkgdir/"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-autopep8() {
  depends=('python2-pep8')

  cd autopep8-py2
  python2 setup.py install -O1 --prefix=/usr --root="$pkgdir/"
  mv "$pkgdir/usr/bin/autopep8" "$pkgdir/usr/bin/autopep8-python2"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
