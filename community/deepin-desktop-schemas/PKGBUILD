# $Id: PKGBUILD 175891 2016-05-20 13:50:37Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-desktop-schemas
pkgver=3.0.5
pkgrel=1
pkgdesc='GSettings deepin desktop-wide schemas'
arch=('any')
url="https://github.com/linuxdeepin/deepin-desktop-schemas"
license=('GPL3')
depends=('dconf' 'deepin-gtk-theme' 'deepin-sound-theme' 'deepin-artwork-themes')
makedepends=('git')
conflicts=('dde-daemon<=2.92.1')
replaces=('deepin-default-gsettings')
groups=('deepin')
source=("git+https://cr.deepin.io/deepin-desktop-schemas.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cd "${pkgname}"
  # fix default background url
  sed -i "s#^picture-uri.*#picture-uri='file:///usr/share/backgrounds/deepin_default_background.jpg'#" overrides/x86/com.deepin.wrap.gnome.desktop.override
  # don't override GNOME defaults
  rm overrides/x86/{org.gnome.desktop,other}.override
}

build() {
  cd "${pkgname}"
  make
}

package() {
  cd "${pkgname}"
  make DESTDIR="${pkgdir}" install
}
