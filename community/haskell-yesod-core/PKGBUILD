# $Id: PKGBUILD 181393 2016-06-27 10:57:31Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=yesod-core
pkgname=haskell-yesod-core
pkgver=1.4.22
pkgrel=1
pkgdesc="Creation of type-safe, RESTful web applications."
url="http://www.yesodweb.com/"
license=("MIT")
arch=('i686' 'x86_64')
depends=("ghc=8.0.1" "haskell-aeson" "haskell-auto-update" "haskell-blaze-builder"
         "haskell-blaze-html" "haskell-blaze-markup" "haskell-byteable" "haskell-case-insensitive"
         "haskell-cereal" "haskell-clientsession" "haskell-conduit" "haskell-conduit-extra"
         "haskell-cookie" "haskell-data-default" "haskell-deepseq-generics" "haskell-exceptions"
         "haskell-fast-logger" "haskell-http-types" "haskell-lifted-base" "haskell-monad-control"
         "haskell-monad-logger" "haskell-mtl" "haskell-mwc-random" "haskell-old-locale"
         "haskell-parsec" "haskell-path-pieces" "haskell-primitive" "haskell-random"
         "haskell-resourcet" "haskell-safe" "haskell-semigroups" "haskell-shakespeare"
         "haskell-text" "haskell-transformers-base" "haskell-unix-compat"
         "haskell-unordered-containers" "haskell-vector" "haskell-wai" "haskell-wai-extra"
         "haskell-wai-logger" "haskell-warp" "haskell-word8")
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('21329336daff5825005efa5905a305e764f2db95e0e43e5d0f0a85fac8bcf124')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
