# $Id: PKGBUILD 173447 2016-05-02 19:28:22Z anatolik $
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Bernhard Walle <bernhard.walle@gmx.de>
# Contributor: Michael Eckert <michael.eckert@linuxmail.org>
# Contributor: Mathieu Pasquet <mathieui@mathieui.net>

pkgname=crash
pkgver=7.1.5
pkgrel=1
pkgdesc='Linux kernel crashdump analysis tool'
url='http://people.redhat.com/~anderson'
arch=(i686 x86_64)
license=(GPL)
depends=(xz ncurses zlib)
source=(http://people.redhat.com/~anderson/crash-$pkgver.tar.gz)
sha256sums=('bb1f6f0a614d188176eb4daf5c44782bf770bdcc9af4024ad5acfc5534e7ae4c')

prepare() {
  cd crash-$pkgver

  make gdb_unzip
  sed -i '/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/' gdb-7.6/libiberty/configure
}

build() {
  cd crash-$pkgver
  make
}

package() {
  cd crash-$pkgver

  mkdir -p "$pkgdir"/usr/bin "$pkgdir"/usr/share/man/man8
  install crash "$pkgdir"/usr/bin/
  install crash.8 "$pkgdir"/usr/share/man/man8/
}
