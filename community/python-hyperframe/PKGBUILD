# $Id: PKGBUILD 170812 2016-04-13 12:28:28Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-hyperframe
pkgname=('python-hyperframe' 'python2-hyperframe')
pkgver=4.0.1
pkgrel=1
pkgdesc="HTTP/2 framing layer for Python"
arch=('any')
license=('MIT')
url="http://hyper.rtfd.org"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("git+https://github.com/python-hyper/hyperframe.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a hyperframe{,-py2}
}

build() {
  cd "$srcdir"/hyperframe
  python setup.py build

  cd "$srcdir"/hyperframe-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/hyperframe
  python setup.py ptr

  cd "$srcdir"/hyperframe-py2
  python2 setup.py ptr
}

package_python-hyperframe() {
  depends=('python')

  cd hyperframe
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-hyperframe() {
  depends=('python2')

  cd hyperframe-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
