# $Id: PKGBUILD 157027 2016-01-17 00:34:49Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Patrick Smits <mail@patricksmits.net>

pkgname=yaws
pkgver=2.0.2
pkgrel=1
pkgdesc='Pure Erlang HTTP web server/framework'
arch=('x86_64' 'i686')
url='http://yaws.hyber.org/'
license=('BSD')
depends=('pam' 'erlang-nox')
backup=('etc/yaws/yaws.conf')
options=('!emptydirs')
install="$pkgname.install"
source=("http://yaws.hyber.org/download/$pkgname-$pkgver.tar.gz"
        "$pkgname.service")
sha256sums=('24dd4aab844e7bb18a9a3cc795b8e8f5755e6f0f8eee3ebb54767ab19eacdde7'
            '09bd1f9f17833c83b4d82d3ac9dc2bf539c8e20ef92d438fced6e70b2aea61bc')

prepare() {
  # Python 2 fix
  for f in cgi.yaws cgi-bin/foo.py; do
    sed -i 's:n/python:n/python2:' "$pkgname-$pkgver/www/$f"
  done
}

build() {
  cd "$pkgname-$pkgver"

  export wwwdir=/srv/http/yaws
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -d "$pkgdir/usr/lib/erlang/lib"
  ln -s /usr/lib/yaws "$pkgdir/usr/lib/erlang/lib/$pkgname-$pkgver"
  install -Dm644 "$srcdir/$pkgname.service" \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "$srcdir/$pkgname-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Path fixups
  cp -ru examples/include "$pkgdir/usr/lib/yaws-$pkgver/examples/"
  mv "$pkgdir/srv/http/yaws/"* "$pkgdir/srv/http/www" || true
  rmdir "$pkgdir/srv/http/yaws" || true
  mv "$pkgdir/srv/http/www" "$pkgdir/srv/http/yaws" || true
}

# getver: yaws.hyber.org/download/?M=r
# vim:set ts=2 sw=2 et:
