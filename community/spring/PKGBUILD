# $Id: PKGBUILD 175380 2016-05-14 13:41:39Z foutrelis $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=spring
pkgver=101.0
pkgrel=2
pkgdesc='A free 3D real-time-strategy (RTS) game engine'
arch=('i686' 'x86_64')
url="http://springrts.com/"
license=('GPL')
depends=('openal' 'glew' 'boost-libs' 'freetype2' 'devil' 'libvorbis' 'sdl2' 'libunwind'
         'libxcursor' 'curl' 'shared-mime-info' 'desktop-file-utils' 'libx11')
makedepends=('boost' 'cmake' 'zip' 'xz' 'p7zip' 'python2' 'jdk8-openjdk' 'mesa')
optdepends=('python2: python-based bots'
            'java-runtime: java-based bots')
install=spring.install
source=(http://downloads.sourceforge.net/sourceforge/springrts/${pkgname}_${pkgver}_src.tar.lzma)
md5sums=('26d7558c86f0af615211f629eaccffa6')

prepare() {
  bsdtar -xf ${pkgname}_${pkgver}_src.tar.lzma
}

build() {
  cd spring_${pkgver}

  cmake	. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDATADIR=share/spring \
    -DJAVA_HOME=/usr/lib/jvm/java-8-openjdk \
    -DCMAKE_SKIP_RPATH=YES
  make
}

package() {
  cd spring_${pkgver}

  make DESTDIR="$pkgdir" install

  install -d "$pkgdir/etc/spring"
  echo '$HOME/.spring' > "$pkgdir/etc/spring/datadir"
}

# vim sw=2:ts=2 et:
