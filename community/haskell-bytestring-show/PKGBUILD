# $Id: PKGBUILD 179563 2016-06-11 06:38:23Z fyan $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-bytestring-show
pkgver=0.3.5.6
pkgrel=8
pkgdesc="Efficient conversion of values into readable byte strings."
url="http://hackage.haskell.org/package/bytestring-show"
license=('BSD')
arch=('i686' 'x86_64')
depends=("ghc")
source=("http://hackage.haskell.org/packages/archive/bytestring-show/${pkgver}/bytestring-show-${pkgver}.tar.gz")
md5sums=('c9041eed86a52e17e6b53a415967f7cb')

build() {
  cd ${srcdir}/bytestring-show-${pkgver}

  # update outdated dependency
  sed -e 's/build-depends: integer-gmp >= 0.2 && < 0.5/build-depends: integer-gmp >= 0.2 \&\& < 0.6/' \
      -e 's/binary < 0.8/binary < 0.9/' \
      -i bytestring-show.cabal

  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
	--prefix=/usr --docdir=/usr/share/doc/${pkgname} \
	--libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  depends=("ghc=`pacman -Q ghc | cut -f2 -d\ |cut -f1 -d-`")

  cd ${srcdir}/bytestring-show-${pkgver}
  install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/bytestring-show
  runhaskell Setup copy --destdir=${pkgdir}
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
