# $Id: PKGBUILD 169513 2016-04-05 06:49:10Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=pari-sage
_pkgver=2.8-2341-g61b65cc
pkgver=${_pkgver//-/.}
pkgrel=3
pkgdesc='Computer algebra system designed for fast computations in number theory. Development snapshot required by SageMath'
url='http://pari.math.u-bordeaux.fr/'
license=(GPL)
arch=(i686 x86_64)
depends=(gmp libx11)
makedepends=(perl texlive-core)
optdepends=('perl: gphelp, tex2mail')
conflicts=(pari)
provides=(pari)
source=("http://mirrors.mit.edu/sage/spkg/upstream/pari/pari-$_pkgver.tar.gz"
	https://raw.githubusercontent.com/sagemath/sage/master/build/pkgs/pari/patches/stackwarn.patch)
md5sums=('03b83e4af898f456cae16c9ade1e1cb5'
         '8cc6dc97128ae6ab965e5d1924d2318c')

prepare() {
  cd pari-${_pkgver}
# Silence warnings when increasing stack size
  patch -p1 -i ../stackwarn.patch
}

build() {
  cd pari-${_pkgver}
  ./Configure --prefix=/usr --with-readline --with-gmp
  make all
}

package() {
  cd pari-${_pkgver}
  make DESTDIR="$pkgdir" install
  ln -sf gp.1.gz "$pkgdir"/usr/share/man/man1/pari.1
}
