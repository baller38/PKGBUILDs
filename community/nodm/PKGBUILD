# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Roel Gerrits <roel@roelgerrits.nl>
pkgname=nodm
pkgver=0.12
pkgrel=1
pkgdesc='X display manager for automatic logins'
arch=('i686' 'x86_64')
url='http://enricozini.org/sw/nodm/'
license=('GPL')
depends=('pam' 'xorg-xinit')
makedepends=('help2man')
backup=('etc/nodm.conf')
source=("https://github.com/spanezz/nodm/archive/debian/${pkgver}-${pkgrel}.tar.gz"
	"nodm.conf"
	"nodm.service"
	"norestart.patch")
md5sums=('a3d2c56e639abd8b626a5f5c016ac813'
         '9a372ab7dcf281963d972ebb9486b605'
         '2660ea3446841ade64db351ab639101c'
         '5076b2fe19551c42e3f5cd8bf445927f')

build() {
	cd "${srcdir}/${pkgname}-debian-${pkgver}-${pkgrel}"

    ./autogen.sh
	./configure --prefix=/usr/
	make
}

package() {
	cd "${srcdir}/${pkgname}-debian-${pkgver}-${pkgrel}"

	install -Dm755 nodm ${pkgdir}/usr/bin/nodm
	install -Dm644 ../nodm.conf ${pkgdir}/etc/nodm.conf
	install -Dm644 ../nodm.service ${pkgdir}/usr/lib/systemd/system/nodm.service
}
