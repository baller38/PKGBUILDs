# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: mk47at

pkgname=rftg
pkgver=0.9.4.q
_pkgver=0.9.4
pkgrel=1
pkgdesc="Singleplayer and online implementation of the card game Race for the Galaxy by Tom Lehman."
arch=('i686' 'x86_64')
url="http://keldon.net/rftg/"
license=('GPL')
depends=('gtk2')
source=("http://keldon.net/rftg/$pkgname-$_pkgver.tar.bz2"
        "https://dl.dropboxusercontent.com/u/7379896/$pkgname/$pkgname-${_pkgver}q-patch.zip"
        "$pkgname.desktop"
        "rftg.svg")

prepare() {
  cd "$srcdir/$pkgname-$_pkgver"
  patch -Np0 < "$srcdir/$pkgname-${_pkgver}q.patch"
}

build() {
  cd "$srcdir/$pkgname-$_pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$_pkgver"
 
  make DESTDIR="$pkgdir/" install
  install -D -m644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -D -m644 "$srcdir/$pkgname.svg" "$pkgdir/usr/share/rftg/$pkgname.svg"
}

md5sums=('065255057b1c60d744c3fed2c93d95bb'
         'd62055acbe62d7fa119267ad42b90506'
         'f2c1a72b0fbf191e564069dfe61d50c4'
         'e0ad78078729229ad1414276348b7d98')
