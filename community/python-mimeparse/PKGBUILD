# $Id: PKGBUILD 171984 2016-04-27 05:16:11Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-mimeparse
pkgname=(python-mimeparse python2-mimeparse)
pkgver=1.5.2
_commit=cc66740c6e46b85ac55e989fd4883d5697e15c35
pkgrel=1
pkgdesc='Module of basic functions for parsing mime-type names and matching them against a list of media-ranges'
arch=('any')
url="https://github.com/dbtsai/python-mimeparse"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
source=("git+https://github.com/dbtsai/python-mimeparse#commit=$_commit")
md5sums=('SKIP')

prepare() {
  cp -a python-mimeparse{,-py2}
}

build() {
  cd "$srcdir"/python-mimeparse
  python setup.py build

  cd "$srcdir"/python-mimeparse-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-mimeparse
  python mimeparse_test.py

  cd "$srcdir"/python-mimeparse-py2
  python2 mimeparse_test.py
}

package_python-mimeparse() {
  depends=('python')

  cd python-mimeparse
  python setup.py install -O1 --root "$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-mimeparse() {
  depends=('python2')

  cd python-mimeparse-py2
  python2 setup.py install -O1 --root "$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
