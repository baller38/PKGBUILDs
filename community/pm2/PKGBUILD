# $Id: PKGBUILD 171871 2016-04-26 02:32:17Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pm2
pkgver=1.1.3
pkgrel=1
pkgdesc='Production process manager for Node.JS applications with a built-in load balancer'
arch=('any')
url='http://pm2.keymetrics.io'
license=('AGPL3')
depends=('nodejs' 'coffee-script' 'semver')
makedepends=('npm' 'git')
source=(http://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz
        pm2.service)
noextract=($pkgname-$pkgver.tgz)
md5sums=('963534cf6e73e7c46940634b9162b3b4'
         '2f3ffbb78aa64d262d99168c5380b472')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  install -Dm644 pm2.service "$pkgdir"/usr/lib/systemd/system/pm2.service
  rm -r "$pkgdir"/usr/etc

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in coffee-script semver; do
    rm -r $dep;
    npm link $dep;
  done
}
