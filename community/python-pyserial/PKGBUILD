# $Id: PKGBUILD 180361 2016-06-17 12:14:02Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Douglas Soares de Andrade <dsandrade@gmail.com>
# Contributor: Douglas Soares de Andrade <dsandrade@gmail.com>

pkgbase=python-pyserial
pkgname=('python-pyserial' 'python2-pyserial')
pkgver=3.1.1
pkgrel=1
pkgdesc="Multiplatform Serial Port Module for Python"
arch=('any')
url="https://github.com/pyserial/pyserial"
license=('custom:PYTHON')
makedepends=('python2' 'python')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pyserial/pyserial/archive/v$pkgver.tar.gz")
md5sums=('911087753ef5d39863a989c87840c51a')

build() {
  cp -a $srcdir/pyserial-${pkgver} $srcdir/py2serial-${pkgver}
}

package_python-pyserial() {
  depends=('python')

  cd $srcdir/pyserial-${pkgver}
  python setup.py install --root=$pkgdir
  install -D -m644 LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-pyserial() {
  depends=('python2')

  cd $srcdir/py2serial-${pkgver}
  python2 setup.py install --root=$pkgdir
  install -D -m644 LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE.txt
  mv $pkgdir/usr/bin/miniterm.py $pkgdir/usr/bin/miniterm2.py
}
