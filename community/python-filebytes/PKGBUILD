# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-filebytes
pkgname=('python-filebytes' 'python2-filebytes')
_pyname=${pkgbase/python-/}
pkgver=0.9.12
pkgrel=1
pkgdesc='Library to read and edit executable binary files'
url='https://github.com/sashs/filebytes'
arch=('any')
license=('GPL2')
makedepends=('python' 'python2' 'python-setuptools' 'python2-setuptools')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/sashs/filebytes/archive/v${pkgver}.tar.gz)
sha512sums=('b5c38a993a5f6d25d9d28c88185a4aae348f91738dad4ef3bb45370d73dc84e514f03d3bd3b99706f5bfacc90d1b5e0506479f00c9ff29bab9161936f4f50ab6')

prepare() {
  cp -ra ${_pyname}-${pkgver}{,-py2}
  sed -r 's|env python$|env python2|' -i ${_pyname}-${pkgver}-py2/samples/*
}

package_python-filebytes() {
  depends=('python')
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 samples/* -t "${pkgdir}/usr/share/doc/${pkgname}/samples"
}

package_python2-filebytes() {
  depends=('python2')
  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 samples/* -t "${pkgdir}/usr/share/doc/${pkgname}/samples"
}

# vim: ts=2 sw=2 et:
