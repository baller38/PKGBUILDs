# $Id: PKGBUILD 174332 2016-05-08 14:00:18Z arojas $
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=samplv1
pkgver=0.7.4
pkgrel=2
pkgdesc="Polyphonic sampler synthesizer with stereo fx"
arch=(i686 x86_64)
url="http://$pkgname.sourceforge.net/$pkgname-index.html"
license=('GPL')
groups=('lv2-plugins')
depends=('jack' 'liblo' 'lv2' 'qt5-base' 'desktop-file-utils'
         'hicolor-icon-theme' 'shared-mime-info')
source=("http://download.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('bb5278771b599f9793609bb763ba7148')

prepare() {
  cd $pkgname-$pkgver

  # x86_64 lib path fix
  sed -i "s/lib64/lib/" src/src_lv2{,ui}.pro
  # Fix build woth GCC 6 (Fedora)
  sed -e "s|^ac_with_paths=.*|ac_with_paths=|g" -i configure configure.ac
}

build() {
  cd $pkgname-$pkgver 
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver 
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
