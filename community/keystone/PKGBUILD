# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=keystone
pkgname=('keystone' 'python-keystone' 'python2-keystone')
pkgver=0.9
pkgrel=1
pkgdesc='Lightweight multi-platform, multi-architecture assembler framework'
url='http://www.keystone-engine.org/'
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('cmake' 'gcc-libs' 'python' 'python2')
options=('staticlibs' '!emptydirs')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/keystone-engine/keystone/archive/${pkgver}.tar.gz)
sha512sums=('170f8f73a7cdd8f049cda3fbc49b968d8ca8a524bbf8ca161a52be4f3c55003b7afd1df8fc118b7838a8484f6128d180f08f09d7a0dcb2c72c3d2009fd59684d')

prepare() {
  cd ${pkgbase}-${pkgver}
  mkdir build-shared build-static
  (cd build-shared
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_SHARED_LIBS=ON \
      -DLLVM_TARGETS_TO_BUILD=all \
      -G "Unix Makefiles" ..
  )
  (cd build-static
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_SHARED_LIBS=OFF \
      -DLLVM_TARGETS_TO_BUILD=all \
      -G "Unix Makefiles" ..
  )
  cp -ra bindings/python{,2}
  sed -r 's|(python)$|\12|' -i bindings/python2/sample.py
}

build() {
  cd ${pkgbase}-${pkgver}
  make -C build-shared
  make -C build-static
  (cd bindings/python
    python setup.py build
  )
  (cd bindings/python2
    python2 setup.py build
  )
}

package_keystone() {
  depends=('gcc-libs')
  cd ${pkgbase}-${pkgver}
  make -C build-shared DESTDIR="${pkgdir}" install
  make -C build-static DESTDIR="${pkgdir}" install
  install -Dm 644 samples/* -t "${pkgdir}/usr/share/doc/${pkgname}/samples"
}

package_python-keystone() {
  depends=('python' 'keystone')
  license=('BSD')
  cd ${pkgbase}-${pkgver}/bindings/python
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 755 sample.py -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-keystone() {
  depends=('python2' 'keystone')
  license=('BSD')
  cd ${pkgbase}-${pkgver}/bindings/python2
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 755 sample.py -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
