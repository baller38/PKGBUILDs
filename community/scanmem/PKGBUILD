# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Michael P <ptchinster@archlinux.us>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Elmo Todurov <todurov@gmail.com>
# Contributor: Alexander De Sousa <archaur.xandy21@spamgourmet.com>

pkgname=scanmem
pkgver=0.15.8
pkgrel=1
pkgdesc="Memory scanner designed to isolate the address of an arbitrary variable in an executing process"
url="https://github.com/scanmem/scanmem"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('gtk3' 'python-gobject' 'polkit')
makedepends=('intltool')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('d43d6967105882db93f39ef80169904366969c7ad3a58798218a7b38cca650f3ef705304143e5afe938c0a1345dd9e3cc33e30888e96f4e2e1cf7519b38713da')

build() {
  cd ${pkgname}-${pkgver}
  ./autogen.sh
  ./configure --enable-gui --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
