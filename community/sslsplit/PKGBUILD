# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=sslsplit
pkgver=0.5.0
pkgrel=1
pkgdesc="Tool for man-in-the-middle attacks against SSL/TLS encrypted network connections"
url="https://www.roe.ch/SSLsplit"
arch=('i686' 'x86_64')
license=('BSD')
depends=('libevent' 'openssl')
checkdepends=('check')
source=(https://mirror.roe.ch/rel/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.asc})
sha512sums=('d8d4f294018a7a28b6e5cdec4690c5078118e1fc9c8b78d626290cdb5f2c8d2ecdbbee776a50666a99c522e9e22a15e85b5a602c412e242ec4cce64327555862'
            'SKIP')
validpgpkeys=('BFF9C7D7EA0EAC7F1AA55B3EFABE3324B5D3397E') # Daniel Roethlisberger <daniel@roe.ch>

build() {
  cd ${pkgname}-${pkgver}
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -j1 test
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX="${pkgdir}/usr" install
  install -Dm 644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm 644 NEWS.md "${pkgdir}/usr/share/doc/${pkgname}/NEWS.md"
}

# vim: ts=2 sw=2 et:
