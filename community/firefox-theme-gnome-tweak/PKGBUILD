# $Id: PKGBUILD 172733 2016-04-29 14:30:03Z heftig $
# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=firefox-theme-gnome-tweak
pkgver=42.1
pkgrel=1
pkgdesc="Firefox extension for customizing the GNOME 3 theme"
url="https://addons.mozilla.org/en-us/firefox/addon/gnome-theme-tweak/"
arch=('any')
license=('MPL')
depends=("firefox-theme-gnome")
makedepends=('python2')
source=("https://addons.cdn.mozilla.net/user-media/addons/458932/gnome_theme_tweak-${pkgver}-fx-linux.xpi")
sha256sums=('a03eac2f4eb5796a9a4e6da07592db08d8a97f4ad09e6769015b6aef6f2ef235')

package() {
  install -m 755 -d "${pkgdir}/usr/lib/firefox/browser/extensions"
  _emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' install.rdf)
  install -m 644 "gnome_theme_tweak-${pkgver}-fx-linux.xpi" "${pkgdir}/usr/lib/firefox/browser/extensions/${_emid}.xpi"
}

