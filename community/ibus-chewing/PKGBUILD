# $Id: PKGBUILD 175309 2016-05-14 05:13:32Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars@gmail.com>
# Contributor: Hiroshi Yui <hiroshiyui@gmail.com>

pkgname=ibus-chewing
pkgver=1.5.0
_cmakefedoraver=2.6.0
pkgrel=1
pkgdesc='Chinese Chewing Engine for IBus Framework'
arch=('i686' 'x86_64')
license=('GPL')
url='http://ibus.googlecode.com'
depends=('ibus' 'libchewing' 'libxtst')
makedepends=('swig' 'gob2' 'cmake' 'git')
source=("git+https://github.com/definite/ibus-chewing.git#tag=$pkgver"
        "git+http://git.fedorahosted.org/git/cmake-fedora.git#tag=$_cmakefedoraver")
sha512sums=('SKIP'
            'SKIP')

prepare() {
  cd $pkgname
  ln -s ../cmake-fedora/Modules ./
}

build() {
  cd $pkgname
  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONF_INSTALL_DIR=/usr/share \
    -DLIBEXEC_DIR=/usr/lib/ibus
  make
  make translations
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
