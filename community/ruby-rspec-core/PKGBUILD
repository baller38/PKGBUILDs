# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Artem Vorotnikov <artem@vorotnikov.me>

_gemname=rspec-core
pkgname=ruby-${_gemname}
pkgver=3.5.0
pkgrel=1
pkgdesc='RSpec runner and formatters'
url='https://github.com/rspec/rspec-core'
arch=('any')
license=('MIT')
depends=('ruby-rspec-support')
options=('!emptydirs')
source=(https://rubygems.org/downloads/${_gemname}-${pkgver}.gem)
noextract=(${_gemname}-${pkgver}.gem)
sha512sums=('484fc350d3672af5a96cd9848df18124f0d1b5f6698819482d78e9948ba6821a987bb783aa98a79a46ecc81f8aac86ea64888c9682f8545a6bc22e84389ead23')

package() {
  local _gemdir="$(gem environment gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 "${pkgdir}${_gemdir}/gems/${_gemname}-${pkgver}/LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 "${pkgdir}${_gemdir}/gems/${_gemname}-${pkgver}/README.md" "${pkgdir}/usr/share/doc/${pkgname}/README"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
