# $Id: PKGBUILD 171603 2016-04-22 18:42:35Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=cysignals
pkgver=1.1.1
pkgrel=1
pkgdesc="Interrupt and signal handling for Cython"
arch=(i686 x86_64)
url="https://www.sagemath.org"
license=(LGPL)
depends=(python2 pari-sage)
makedepends=(python2-sphinx cython2)
source=("https://github.com/sagemath/cysignals/releases/download/$pkgver/$pkgname-$pkgver.tar.bz2")
md5sums=('20483b618a1d5c65485d38a2d0002386')

prepare() {
  sed -e 's|sphinx-build|sphinx-build2|g' -i $pkgname-$pkgver/docs/Makefile
}

build() {
  cd $pkgname-$pkgver
  python2 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --root="$pkgdir"
}

