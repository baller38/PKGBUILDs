# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=scapy3k
_pkgname=scapy
pkgver=0.18
pkgrel=1
pkgdesc="A powerful interactive packet manipulation program written in Python"
url="https://github.com/phaethon/scapy"
depends=('tcpdump' 'python' )
optdepends=('python-crypto: WEP decryption support'
            'python-pyx: psdump() and pdfdump() functions'
            'python-gnuplot: plotting support'
            'graphviz: conversations() method support'
            'sox: for VOIP support')
checkdepends=('python-crypto' 'python-pyx' 'python-gnuplot' 'graphviz' 'sox')
arch=('any')
license=('GPL2')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/phaethon/scapy/archive/v${pkgver}.tar.gz)
sha512sums=('1ba8c606156e663001a7f19805f0a41b6415d9c78840ba2009726a6a9b01bdf6f90e6b13a4473201174ee097579a3c10a708ae56b10adde14ffce1a0054fce30')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -r 's|((bin\|doc)/(UT)?scapy)|\13k|g' -i setup.py
  mv doc/scapy.1.gz doc/scapy3k.1.gz
  mv bin/scapy{,3k}
  mv bin/UTscapy{,3k}
}

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}/test
  # Disabled tests as they currently hang
  #./run_tests
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --skip-build --root="${pkgdir}" --prefix=/usr
  install -Dm 644 doc/scapy/*.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
