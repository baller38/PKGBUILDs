# $Id: PKGBUILD 169509 2016-04-05 06:49:04Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

pkgname=coin-or-cgl
pkgver=0.59.9
pkgrel=4
pkgdesc="COIN-OR Cut Generation Library"
arch=(i686 x86_64)
url="https://projects.coin-or.org/Cgl"
license=(EPL)
groups=(coin-or)
depends=(coin-or-clp)
source=("http://www.coin-or.org/download/source/Cgl/Cgl-${pkgver}.tgz")
sha1sums=('ebe87d6ba1cab31a8223c187fe8e7f7f141989d9')

build() {
  cd Cgl-$pkgver/Cgl
  COIN_SKIP_PROJECTS="Sample" \
  ./configure --prefix=/usr \
              --with-osi-lib="$(pkg-config --libs osi)" \
              --with-osi-incdir="/usr/include/coin/" \
              --with-clp-lib="$(pkg-config --libs clp)" \
              --with-clp-incdir="/usr/include/coin/" \
              --with-coinutils-lib="$(pkg-config --libs coinutils)" \
              --with-coinutils-incdir="/usr/include/coin/" \
              --enable-dependency-linking
  make
}

check() {
  cd Cgl-$pkgver/Cgl
  make test
}

package() {
  cd Cgl-$pkgver/Cgl
  PKG_CONFIG_LIBDIR="$pkgdir"/usr/lib/pkgconfig/ \
  make DESTDIR="$pkgdir" install
}
