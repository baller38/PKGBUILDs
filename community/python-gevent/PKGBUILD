# $Id: PKGBUILD 70305 2012-05-04 08:21:08Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgbase=python-gevent
pkgname=(python-gevent python2-gevent)
pkgver=1.1.1
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Python network library that uses greenlet and libev for easy and scalable concurrency"
license=("MIT")
url="http://www.gevent.org/"
makedepends=('python-greenlet' 'python2-greenlet' 'cython' 'cython2' 'git')
checkdepends=('lsof')
source=("git+https://github.com/gevent/gevent.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  sed -i 's/C.UTF-8/en_US.UTF-8/' gevent/Makefile
  cp -a gevent{,-py2}
}

build() {
  cd "$srcdir"/gevent

  EMBED=1 \
  CYTHON=cython \
  PYTHON=python \
  python setup.py build

  cd "$srcdir"/gevent-py2

  EMBED=1 \
  CYTHON=cython2 \
  PYTHON=python2 \
  python2 setup.py build
}

check() {
  cd "$srcdir"/gevent
  PYTHON=python PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.5:$PYTHONPATH" make fulltoxtest || warning "Tests failed"

  cd "$srcdir"/gevent-py2
  PYTHON=python2 PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" make fulltoxtest || warning "Tests failed"
}

package_python-gevent() {
  depends=('python-greenlet')

  cd gevent
  python setup.py install -O1 --root="$pkgdir"
  install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-gevent() {
  depends=('python2-greenlet')

  cd gevent-py2
  python2 setup.py install -O1 --root="$pkgdir"
  install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
