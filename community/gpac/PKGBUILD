# $Id: PKGBUILD 165235 2016-03-06 10:40:57Z pierre $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gpac
pkgver=0.5.2
pkgrel=6
epoch=1
pkgdesc="A multimedia framework based on the MPEG-4 Systems standard"
arch=('i686' 'x86_64')
url="http://gpac.sourceforge.net"
license=('LGPL')
depends=('ffmpeg' 'glu' 'libxv')
makedepends=('git' 'jack' 'a52dec' 'freetype2' 'faad2' 'libmad' 'mesa' 'sdl2')
optdepends=('jack: for jack support'
	    'a52dec: for A52 support'
            'faad2: for AAC support'
	    'libmad: for mp3 support'
	    'sdl2: for sdl support')
options=('staticlibs' '!makeflags')
source=(git://github.com/gpac/gpac.git#tag=v$pkgver gpac-ffmpeg3.patch)
sha1sums=('SKIP'
          '2ce98b1d8aa2f8091c6581d189470250502484e7')

prepare() {
  cd $pkgname
# Fix build with ffmpeg 3.0 (Gentoo)
  patch -p1 -i ../gpac-ffmpeg3.patch
}

build() {
  cd ${pkgname}
  ./configure --prefix=/usr --mandir=/usr/share/man --X11-path=/usr --use-js=no
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install install-lib
}
