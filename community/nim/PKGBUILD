# $Id: PKGBUILD 121061 2014-10-20 12:57:19Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Dominik Picheta <morfeusz8@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jesus Alvarez <jeezusjr@gmail.com>

pkgname=nim
pkgver=0.14.2
pkgrel=1
pkgdesc='Imperative, multi-paradigm, compiled programming language'
arch=('x86_64' 'i686')
url='http://nim-lang.org/'
license=('MIT')
makedepends=('git' 'glib2')
options=('!emptydirs')
source=("$pkgname::git://github.com/Araq/Nimrod.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cd "$pkgname"

  rm build/empty.txt && rmdir build
  git clone --depth=1 git://github.com/nimrod-code/csources build
  cd build
  git checkout b39a1df
}

build() {
  cd "$pkgname/build"

  sh build.sh

  cd ..
  ./bin/nim c koch
  ./koch boot -d:release -d:useGnuReadline

  export PATH="$srcdir/$pkgname/bin:$PATH"

  cd lib
  nim c --app:lib -d:createNimRtl -d:release nimrtl.nim

  cd ../tools
  nim c -d:release nimgrep.nim
  #nim c -d:release nimrepl.nim # does not compile
}

package() {
  cd "$pkgname"

  export PATH="$srcdir/$pkgname/bin:$PATH"

  ./koch install "$pkgdir"

  install -d "$pkgdir/usr/share/nim/doc" "$pkgdir/usr/lib/nim" \
    "$pkgdir/etc" "$pkgdir/usr/bin"

  cd "$pkgdir/nim"
  mv "lib/"* "$pkgdir/usr/lib/nim/"
  mv "config/"* "$pkgdir/etc/"
  cp -a "$srcdir/$pkgname/lib/packages" "$pkgdir/usr/lib/nim/"
  mv "bin/"* "$pkgdir/usr/bin/"

  cd "$srcdir/$pkgname"
  rm -r "$pkgdir/nim"
  mv examples web "$pkgdir/usr/share/nim/doc/"
  mv "doc/"* "$pkgdir/usr/share/nim/doc/"
  install -m644 "lib/libnimrtl.so" "$pkgdir/usr/lib/libnimrtl.so"
  install -m755 "tools/nimgrep" "$pkgdir/usr/bin/"
  #install -m755 "tools/nimrepl" "$pkgdir/usr/bin/"
  install -Dm644 "copying.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Fix FS#48118, related to the doc2 command
  ln -s /usr/share/nim/doc "$pkgdir/usr/lib/nim/doc"
}

# vim:set ts=2 sw=2 et:
