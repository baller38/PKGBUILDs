# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Lance Chen <cyen0312@gmail.com>

pkgname=enjarify
pkgver=1.0.2
pkgrel=1
pkgdesc="Translates Dalvik bytecode to equivalent Java bytecode"
url="https://github.com/google/enjarify"
arch=('any')
license=('Apache')
depends=("python")
makedepends=('git')
checkdepends=('java-runtime')
source=(${pkgname}::"git+https://github.com/google/enjarify#tag=${pkgver}")
sha512sums=('SKIP')

prepare() {
  cd ${pkgname}
  sed -r 's| -Xss515m||g' -i enjarify/runtests.py
}

build() {
  cd ${pkgname}
  python -m compileall .
  python -O -m compileall .
}

check() {
  cd ${pkgname}
  LC_CTYPE=en_US.UTF-8 python -m enjarify.runtests
}

package() {
  cd ${pkgname}
  install -d "${pkgdir}/usr/lib/enjarify"
  cp -R enjarify enjarify.sh "${pkgdir}/usr/lib/enjarify"
  install -d "${pkgdir}/usr/bin"
  ln -s /usr/lib/enjarify/enjarify.sh "${pkgdir}/usr/bin/enjarify"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
