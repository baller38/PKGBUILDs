# $Id: PKGBUILD 163982 2016-03-01 14:54:05Z anatolik $
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Alexandre Bique <bique.alexandre@gmail.com>

pkgname=re2
_tag='2016-03-01'
pkgver=${_tag//-/}
pkgrel=1
pkgdesc='C++ library for fast, safe, thread-friendly alternative to backtracking regular expression engines like those used in PCRE, Perl, and Python'
url='https://github.com/google/re2'
depends=(gcc-libs)
arch=(i686 x86_64)
license=(BSD)
source=(re2-$pkgver.zip::https://github.com/google/re2/archive/$_tag.zip)
sha1sums=('f66df774723add0b5bae23aef69ff64400d36365')

build() {
  cd re2-$_tag
  LDFLAGS+=" -pthread"
  CXXFLAGS+=" -std=c++11"
  make prefix=/usr
}

check() {
  cd re2-$_tag
  LDFLAGS+=" -pthread"
  make prefix=/usr test
}

package() {
  cd re2-$_tag
  make prefix=/usr DESTDIR="$pkgdir" install
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
