# $Id: PKGBUILD 169543 2016-04-06 02:26:48Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Micha Alt <micha.tucker@gmail.com> 
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Tom Vincent <http://tlvince.com/contact>

pkgname=bower
pkgver=1.7.9
pkgrel=1
pkgdesc='A package manager for the web'
arch=('any')
url='http://bower.io/'
license=('MIT')
depends=('nodejs' 'semver' 'uglify-js')
makedepends=('npm' 'git')
source=(http://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz
        LICENSE)
noextract=($pkgname-$pkgver.tgz)
md5sums=('da482af431c5906e45c7372fc6e4f160'
         '135697567327f92e904ef0be2082da5e')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/lib/node_modules
  for dep in semver uglify-js; do
    rm -r $dep;
    npm link $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/lib/node_modules/package-json/node_modules
  for dep in semver; do
    rm -r $dep;
    npm link $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/lib/node_modules/semver-diff/node_modules
  for dep in semver; do
    rm -r $dep;
    npm link $dep;
  done
}
