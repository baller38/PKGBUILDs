# $Id: PKGBUILD 181333 2016-06-27 07:09:25Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=pointed
pkgname=haskell-pointed
pkgver=5
pkgrel=19
pkgdesc="Pointed and copointed data"
url="http://github.com/ekmett/pointed/"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=("ghc=8.0.1" "haskell-comonad" "haskell-data-default-class" "haskell-hashable"
         "haskell-kan-extensions" "haskell-semigroupoids" "haskell-semigroups"
         "haskell-stm" "haskell-tagged" "haskell-transformers-compat"
         "haskell-unordered-containers")
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('8906b8af5125ab3376794a290c5484dbec5a35d0bd0a57e94392ec0e12535d17')

prepare() {
    sed -i 's/data-default-class >= 0.0.1 && < 0.1/data-default-class >= 0.0.1 \&\& < 0.2/' ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -fcomonad -fcontainers -fkan-extensions -fsemigroupoids -fsemigroups \
            -fstm -ftagged -ftransformers -funordered-containers
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
