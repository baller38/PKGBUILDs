# $Id: PKGBUILD 177977 2016-06-05 12:11:21Z jelle $
# Maintainer: SÃ©bastien Luttringer

pkgbase=python-psutil
pkgname=('python-psutil' 'python2-psutil')
pkgver=4.2.0
pkgrel=1
arch=('i686' 'x86_64')
url='https://github.com/giampaolo/psutil'
license=('custom: BSD')
makedepends=('python' 'python-setuptools' 'python2' 'python2-setuptools')
checkdepends=('net-tools' 'procps-ng' 'python2-nose')
source=("https://github.com/giampaolo/psutil/archive/release-$pkgver.tar.gz")
md5sums=('33571543baf17f5c09c9bcdc4cf11b41')

build() {
  cd psutil-release-$pkgver

  msg2 python
  python setup.py build --build-lib=build/python

  msg2 python2
  python2 setup.py build --build-lib=build/python2
  find build/python2 -type f -exec \
    sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \;
}

check() {
  cd psutil-release-$pkgver

  msg2 python
  export PYTHONPATH="$PWD/build/python"
  # FIXME: requires https://github.com/giampaolo/psutil/issues/538#issuecomment-68549637 to be resolved.
  #python tests/test_psutil.py

  msg2 python2
  export PYTHONPATH="$PWD/build/python2"
  #nosetests2 tests/
}

package_python-psutil() {
  pkgdesc='A cross-platform process and system utilities module for Python'
  depends=('glibc' 'python')

  cd psutil-release-$pkgver
  python setup.py build --build-lib=build/python \
                  install --root="$pkgdir" --optimize=1
  install -D -m 644 LICENSE "$pkgdir/"usr/share/licenses/$pkgname/LICENSE
}

package_python2-psutil() {
  pkgdesc='A cross-platform process and system utilities module for Python2'
  depends=('glibc' 'python2')

  cd psutil-release-$pkgver
  python2 setup.py build --build-lib=build/python2 \
                   install --root="$pkgdir" --optimize=1
  install -D -m 644 LICENSE "$pkgdir/"usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
