# $Id: PKGBUILD 176198 2016-05-24 08:22:39Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=xtrabackup
pkgver=2.4.3
_boost_ver=1.59.0
pkgrel=1
pkgdesc='Non-blocking backup tool for MySQL'
arch=('i686' 'x86_64')
url='http://www.percona.com/software/percona-xtrabackup/'
license=('GPL')
depends=('libaio' 'libev' 'libgcrypt' 'curl' 'perl-dbd-mysql')
makedepends=('cmake' 'python-sphinx' 'zlib' 'vim' 'libedit' 'libevent')
optdepends=('qpress: for compressed backups')
source=(https://www.percona.com/downloads/XtraBackup/Percona-XtraBackup-$pkgver/source/tarball/percona-xtrabackup-$pkgver.tar.gz
        http://sourceforge.net/projects/boost/files/boost/${_boost_ver}/boost_${_boost_ver//./_}.tar.gz)
sha256sums=('dce53c81ad186cf83c8c513d2051cb0029c9ebc7453c7a5c24c78e4bcd1a121d'
            '47f11c8844e579d02691a607fbd32540104a9ac7a2534a8ddaef50daf502baac')
build() {
  cd percona-xtrabackup-$pkgver
  cmake -DBUILD_CONFIG=xtrabackup_release \
        -DBUILD_TESTING=OFF \
        -DCMAKE_BUILD_TYPE=Release \
        -DMYSQL_DATADIR=/var/lib/mysql \
        -DMYSQL_UNIX_ADDR=/run/mysqld/mysqld.sock \
        -DWITH_SSL=system \
        -DWITH_LIBEVENT=system \
        -DWITH_EDITLINE=system \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_BOOST="../boost_${_boost_ver//./_}"
  make
}

package() {
  cd percona-xtrabackup-$pkgver
  make DESTDIR="$pkgdir" install

  cd  "$pkgdir"
  rm -rf usr/xtrabackup-test
  install -dm755 usr/share
  mv build/$pkgname/src/percona-xtrabackup-$pkgver/man usr/share
  rm -rf build
}
