# $Id: PKGBUILD 181354 2016-06-27 07:09:46Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=DAV
pkgname=haskell-dav
pkgver=1.2
pkgrel=57
pkgdesc="RFC 4918 WebDAV support"
url="http://floss.scru.org/hDAV"
license=("GPL3")
arch=('i686' 'x86_64')
depends=("ghc=8.0.1" "haskell-case-insensitive" "haskell-data-default" "haskell-exceptions"
         "haskell-http-client" "haskell-http-client-tls" "haskell-http-types" "haskell-lens"
         "haskell-mtl" "haskell-network" "haskell-network-uri" "haskell-optparse-applicative"
         "haskell-transformers-base" "haskell-transformers-compat" "haskell-utf8-string"
         "haskell-xml-conduit" "haskell-xml-hamlet")
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('1a07c8ef17cd4207c6f889e7eb5c45d9f025374f2cdacc1e34e6d71fe103b746')

prepare() {
    sed -i '/mtl-compat/d' ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -fnetwork-uri
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
