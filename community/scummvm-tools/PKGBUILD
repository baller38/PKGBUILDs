# $Id: PKGBUILD 171502 2016-04-21 10:52:23Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jérémie Dimino <jeremie@dimino.org>
# Contributor: Sebastian Hase <sebastian_hase@gmx.de>
# Contributor: Joe Davison <joe@thosedamnpeanuts.com>
pkgname=scummvm-tools
pkgver=1.8.0
pkgrel=1
pkgdesc="A set of tools that enable you to convert, compress and decode game datafiles for ScummVM"
url="http://www.scummvm.org/"
license=('GPL')
depends=('libpng' 'wxgtk2.8' 'flac' 'libvorbis')
arch=('i686' 'x86_64')
source=("https://www.scummvm.org/frs/scummvm-tools/${pkgver}/scummvm-tools-${pkgver}.tar.xz")
md5sums=('47cb6462a9eb985e22c801445a6ffdc5')

build() {
  cd ${pkgname}-${pkgver}

  sed -i 's/wxgtk2-2.8-config/wx-config-2.8/' configure
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}

  install -d ${pkgdir}/usr/bin

  make DESTDIR=${pkgdir} install

  install -Dm 644 README ${pkgdir}/usr/share/doc/scummvm-tools/README
  install -Dm 644 convert_dxa.sh ${pkgdir}/usr/share/doc/scummvm-tools/samples/convert_dxa.sh
}
#vim: sw=2 ts=2 et:
