# $Id: PKGBUILD 177475 2016-05-30 18:30:35Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: GordonGR <ntheo1979@gmail.com>

pkgname=lib32-gtk3
pkgver=3.20.6
pkgrel=1
pkgdesc='GObject-based multi-platform GUI toolkit'
arch=('x86_64')
license=('LGPL')
url='http://www.gtk.org/'
depends=('gtk3' 'lib32-at-spi2-atk' 'lib32-colord' 'lib32-gdk-pixbuf2'
         'lib32-json-glib' 'lib32-libcups' 'lib32-libepoxy' 'lib32-librsvg'
         'lib32-libxcomposite' 'lib32-libxcursor' 'lib32-libxinerama'
         'lib32-libxkbcommon' 'lib32-libxrandr' 'lib32-pango' 'lib32-rest')
makedepends=('gcc-multilib' 'gobject-introspection' 'gtk-doc')
source=("https://download.gnome.org/sources/gtk+/${pkgver:0:4}/gtk+-$pkgver.tar.xz"
        'gtk-query-immodules-3.0-32.hook')
sha256sums=('3f8016563a96b1cfef4ac9e795647f6316deb2978ff939b19e4e4f8f936fa4b2'
            '6b786bbcb83ba850675339b42690eedba110dcd8d0c3cdb5cfef333d8f9c320b')

prepare() {
  cd gtk+-${pkgver}

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd gtk+-${pkgver}

  export CC='gcc -m32'
  export CXX='/bin/false'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --localstatedir='/var' \
    --sysconfdir='/etc' \
    --enable-{broadway,wayland,x11}-backend \
    --disable-libcanberra \
    --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd gtk+-${pkgver}

  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/usr/bin/gtk-query-immodules-3.0{,-32}
  mv "${pkgdir}"/usr/bin/gtk-query-settings{,-32}
  rm "${pkgdir}"/usr/bin/{broadwayd,gtk-{builder-tool,encode-symbolic-svg,launch,update-icon-cache},gtk3-{demo,demo-application,icon-browser,widget-factory}}
  rm -rf "${pkgdir}"/{etc,usr/{include,share}}

  install -dm 755 "${pkgdir}"/usr/share/libalpm/hooks
  install -m 644 ../gtk-query-immodules-3.0-32.hook "${pkgdir}"/usr/share/libalpm/hooks/
}

# vim: ts=2 sw=2 et:
