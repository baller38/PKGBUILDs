# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='cewl'
pkgver='5.1'
pkgrel=1
groups=('blackarch' 'blackarch-automation' 'blackarch-cracker')
pkgdesc='A custom word list generator'
arch=('any')
url="http://www.digininja.org/projects/cewl.php"
license=('CCPL')
depends=('ruby' 'ruby-spider' 'ruby-mini_exiftool' 'ruby-nokogiri'
         'ruby-http_configuration' 'ruby-rubyzip' 'ruby-zip' 'ruby-mime-types')
source=("http://www.digininja.org/files/cewl_${pkgver}.tar.bz2")
install='cewl.install'
sha1sums=('d3ed1ea3fd13f7343cf55e00e815ddc7a899073c')

package() {
  cd "$srcdir/cewl"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/cewl"

  install -Dm644 README "$pkgdir/usr/share/doc/cewl/README"

  rm README

  cp -a * "$pkgdir/usr/share/cewl"

  cat > "$pkgdir/usr/bin/cewl" << EOF
#!/bin/sh
cd /usr/share/cewl
exec ruby cewl.rb "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/cewl"

  cat > "$pkgdir/usr/bin/cewl-fab" << EOF
#!/bin/sh
cd /usr/share/cewl
exec ruby fab.rb "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/cewl-fab"
}
