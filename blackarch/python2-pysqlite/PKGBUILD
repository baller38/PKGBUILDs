# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='python2-pysqlite'
pkgver='2.8.1'
pkgrel=1
pkgdesc='A Python DB-API 2.0 interface for the SQLite embedded relational database engine.'
license=('custom')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://pypi.python.org/pypi/pysqlite"
depends=('python2' 'sqlite')
conflicts=('python-pysqlite<=2.6.3-2')
replaces=('python-pysqlite<=2.6.3-2')
source=("https://pypi.python.org/packages/source/p/pysqlite/pysqlite-${pkgver}.tar.gz"
        'setup.cfg')
sha1sums=('ea2c7946400305a92b7670ec6758c012490db70f'
          'a483c9b2eb83269d22475c9b2002baf81fdc6256')

prepare() {
  cd "$srcdir"

  cp "$srcdir/setup.cfg" pysqlite-$pkgver
}

build() {
  cd "$srcdir/pysqlite-$pkgver"

  python2 setup.py build
}


package() {
  cd "$srcdir/pysqlite-$pkgver"

  python2 setup.py install --root="${pkgdir}"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  rm -r "${pkgdir}/usr/pysqlite2-doc"
}
