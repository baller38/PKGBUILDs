# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='hashcat'
pkgver='3.00'
pkgrel=1
groups=('blackarch' 'blackarch-cracker')
pkgdesc='A multithreaded cross platform hash cracker.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://hashcat.net/hashcat/'
license=('custom:unknown')
makedepends=('p7zip')
depends=('glibc')
source=("https://hashcat.net/files/hashcat-${pkgver}.7z")
sha1sums=('fb295f2b8443dfa131c5224297d3c85fc4fc02c6')

prepare() {
  cd "$srcdir/hashcat-$pkgver"

  if [[ "$CARCH" == 'x86_64' ]]; then
    mv hashcat64.bin hashcat
  else
    mv hashcat32.bin hashcat
  fi
}

package() {
  cd "$srcdir/hashcat-$pkgver"

  mkdir -p "$pkgdir/usr/share/hashcat"

  install -Dm755 hashcat "$pkgdir/usr/bin/hashcat"

  install -Dm644 docs/license.txt \
    "$pkgdir/usr/share/licenses/hashcat/license.txt"

  rm docs/license.txt

  install -Dm644 -t "$pkgdir/usr/share/doc/hashcat/" docs/*

  rm -rf docs

  cp -a * "$pkgdir/usr/share/hashcat"
}
