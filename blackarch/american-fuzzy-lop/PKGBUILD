# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='american-fuzzy-lop'
pkgver='1.79b'
pkgrel=1
groups=('blackarch' 'blackarch-fuzzer' 'blackarch-binary')
pkgdesc='A practical, instrumentation-driven fuzzer for binary formats.'
arch=('i686' 'x86_64')
url='https://code.google.com/p/american-fuzzy-lop/'
license=('apache')
depends=('glibc')
source=("http://lcamtuf.coredump.cx/afl/releases/afl-${pkgver}.tgz")
sha1sums=('167250d80ab7956c3d72b701fdea43e3537c1143')

prepare() {
 cd "$srcdir/afl-$pkgver"

 sed -i 's|/usr/local|/usr|g' Makefile
}

build() {
  cd "$srcdir/afl-$pkgver"

  make PREFIX="/usr"
}

package() {
  cd "$srcdir/afl-$pkgver"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/lib/afl"
  mkdir -p "$pkgdir/usr/share/doc/afl"

  make DESTDIR="$pkgdir" install

  cp -a docs/* "$pkgdir/usr/share/doc/afl"
}
