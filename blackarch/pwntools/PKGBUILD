# This file is part of BlackArch Linux ( http://blackarch.org ).
# See COPYING for license details.

pkgname='pwntools'
pkgver='2.2.0'
pkgrel=3
pkgdesc='The CTF framework used by #Gallopsled in every CTF.'
arch=('any')
url='https://github.com/Gallopsled/pwntools'
license=('MIT' 'GPL2' 'BSD')
groups=('blackarch' 'blackarch-disassembler' 'blackarch-reversing')
depends=('python2' 'python2-paramiko' 'python2-mako' 'python2-pyelftools'
         'python2-capstone' 'python2-pyserial' 'python2-requests'
         'python2-psutil' 'ropgadget')
makedepends=('python2-setuptools')
source=("https://github.com/Gallopsled/pwntools/archive/${pkgver}.tar.gz")
sha1sums=('08cc451b02f03417bc0795a556bdc10be9ddcba3')

prepare() {
  cd "$srcdir/pwntools-$pkgver"

  sed -i 's|capstone==2.1|capstone|' setup.py
}

build() {
  cd "$srcdir/pwntools-$pkgver"

  python2 setup.py build
}

package() {
  cd "$srcdir/pwntools-$pkgver"

  install -Dm644 LICENSE-pwntools.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-pwntools.txt"

  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir/usr/bin/asm" "$pkgdir/usr/bin/pwntools-asm"
  mv "$pkgdir/usr/bin/disasm" "$pkgdir/usr/bin/pwntools-disasm"
}
